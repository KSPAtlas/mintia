#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"
#include "<inc>/HALLog.h"

#include "<inc>/HALDriver.h"

#include "<inc>/HALRTC.h"

#include "<inc>/HALLIMNstationCitron.h"

#include "<inc>/HALInterrupt.h"

#include "<inc>/KeIPL.h"

var DriverRTCPortCmd 0
var DriverRTCPortData 0
var DriverRTCInterruptNumber 0

externptr HALRTCQueryFunction

const RTCINTERVAL 10

const RTCCMDINTERVAL 1
const RTCCMDQUERYSEC 2
const RTCCMDQUERYMS 3

extern DriverRTCInterrupt { int -- }

fn (FDriverInitEarly) DriverInitEarly { arg1 arg2 arg3 arg4 -- ok }
	"clock,cmdPort" a3xDGetProperty DriverRTCPortCmd!

	"clock,dataPort" a3xDGetProperty DriverRTCPortData!

	"interrupt#" a3xDGetProperty DriverRTCInterruptNumber!

	pointerof DriverRTCQuery HALRTCQueryFunction!

	pointerof DriverRTCInterrupt DriverRTCInterruptNumber@ HALInterruptRegister

	0 ok!
end

fn (FDriverInit) DriverInit { -- ok }
	// start the clock interrupt

	RTCINTERVAL DriverRTCPortData@ HALLIMNstationCitronOutl
	RTCCMDINTERVAL DriverRTCPortCmd@ HALLIMNstationCitronCommand

	0 ok!
end

fn (HALRTCQueryF) DriverRTCQuery { -- sec ms }
	auto ipl
	IPLCLOCK KeIPLRaise ipl!

	RTCCMDQUERYSEC DriverRTCPortCmd@ HALLIMNstationCitronCommand
	DriverRTCPortData@ HALLIMNstationCitronInl sec!

	RTCCMDQUERYMS DriverRTCPortCmd@ HALLIMNstationCitronCommand
	DriverRTCPortData@ HALLIMNstationCitronInl ms!

	ipl@ KeIPLLower
end

asm "

DriverRTCInterrupt:
	ret

"