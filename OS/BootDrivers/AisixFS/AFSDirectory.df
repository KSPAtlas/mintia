#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/IO.h"

#include "<inc>/FSRTL.h"

#include "AisixFS.h"

fn AFSDirectoryGetEntry { seek afsdirent fcb -- nextseek ok }
	// FCB is locked by caller

	if (DEBUGCHECKS)
		if (fcb@ IOFileControlBlock_FileType + @ OSFILETYPE_DIRECTORY ~=)
			// should have been caught by the IO system code

			"AFSReadDirectory: not a directory\n" KeCrash
		end
	end

	auto cacheblock
	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

	if (cacheblock@ ~~)
		// initialize caching on directory FCB

		1 // wantcaching
		fcb@ // fcb
		IOFileControlBlockCacheCheck ok! cacheblock!

		if (ok@)
			return
		end
	end

	auto mount
	fcb@ IOFileControlBlock_DeviceObject + @ mount!

	auto byteoff
	seek@ AFSDirEnt_SIZEOF * byteoff!

	auto bytesize
	fcb@ IOFileControlBlock_SizeInBytes + @ bytesize!

	auto pageno
	byteoff@ PAGESHIFT >> pageno!

	auto curpageno
	-1 curpageno!

	auto pfdbe
	0 pfdbe!

	auto data

	while (byteoff@ bytesize@ <)
		if (pageno@ curpageno@ ~=)
			// must read in a new page

			if (pfdbe@)
				pfdbe@ MmEvictablePageDereference drop
			end

			0 // flags
			0 // kflags
			byteoff@ PAGENUMBERMASK & // offset
			fcb@ // fcb
			IOCachePageRead ok! pfdbe! data!

			if (ok@)
				return
			end

			byteoff@ PAGEOFFSETMASK & data +=

			pageno@ curpageno!
		end

		if (data@ AFSDirEnt_INum + @)
			// found a dirent

			afsdirent@ AFSDirEnt_name + // dest
			data@ AFSDirEnt_name + // src
			63 // max
			strncpy

			data@ AFSDirEnt_INum + @ afsdirent@ AFSDirEnt_INum + !

			pfdbe@ MmEvictablePageDereference drop

			0 ok!

			seek@ 1 + nextseek!

			return
		end

		AFSDirEnt_SIZEOF data +=
		AFSDirEnt_SIZEOF byteoff +=
		byteoff@ PAGESHIFT >> pageno!
		1 seek +=
	end

	if (pfdbe@)
		pfdbe@ MmEvictablePageDereference drop
	end

	seek@ nextseek!
	STATUS_IO_END_OF_FILE ok!
end

fn (IODispatchReadDirectoryFunction) AFSReadDirectory { seek dirent fcb -- nextseek ok }
	// FCB is locked by caller

	auto afsdirent
	AFSDirEnt_SIZEOF alloc afsdirent!

	seek@ // seek
	afsdirent@ // afsdirent
	fcb@ // fcb
	AFSDirectoryGetEntry ok! nextseek!

	if (ok@)
		return
	end

	dirent@ OSDirectoryEntry_Name + // dest
	afsdirent@ AFSDirEnt_name + // src
	63 // max
	strncpy
end