#include "<df>/dragonfruit.h"

#include "<ll>/rta3x/a3x.h"

#include "../Loader/LoaderGlobal.h"

#include "<inc>/HALConsole.h"
#include "<inc>/DebugTrace.h"
#include "<inc>/DebugSymbols.h"

externptr HALLoaderInfo

var DebugLink 0

asm "

GetLink:
	la   t0, DebugLink
	mov  long [t0], sp
	ret

"

extern GetLink { -- }

fn DebugDump { rows cols -- }
	GetLink

	auto link
	DebugLink@ link!

	pointerof DebugDump link@ rows@ cols@ DebugTrace
end

buffer TraceNameBuffer 128

fn DebugTrace { pc link rows cols -- }
	"\n" Printf

	1 rows -=

	auto infocols
	cols@ 80 / infocols!

	auto i
	infocols@ i!

	while (i@)
		"StackPtr InstrPtr DWORD+08 DWORD+12 Name                              " Printf
		1 i -=
	end

	"\n" Printf

	1 rows -=

	auto links
	0 links!

	if (link@ 0 ==)
		"zero link!\n" Printf
		return
	end

	while (link@@ rows@ &&)
		infocols@ i!

		while (link@@ i@ &&)
			if (links@ 128 >=)
				" maxtrace!\n" Printf
				return
			end

			if (link@ 3 &)
				" unaligned!\n" Printf
				return
			end

			auto symname
			auto symbase
			auto dll

			pc@ DebugGetSymDLL symname! symbase! dll!

			if (symbase@ ~~)
				TraceNameBuffer "UNKNOWN" strcpy
			end else
				if (symname@ ~~)
					"NAMELESS" symname!
				end

				TraceNameBuffer	dll@ DLL_Name + strcpy
				TraceNameBuffer TraceNameBuffer strlen + "!" strcpy
				TraceNameBuffer TraceNameBuffer strlen + symname@ strcpy
			end

			TraceNameBuffer
			link@ 12 + @
			link@ 8 + @
			pc@
			link@ "%08x %08x %08x %08x %33s " Printf

			link@ 4 + @ pc!
			link@@ link!

			1 i -=
			1 links +=
		end

		"\n" Printf

		1 rows -=
	end
end