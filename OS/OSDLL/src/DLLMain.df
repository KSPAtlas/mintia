#include "<df>/dragonfruit.h"

#include "<inc>/OSCalls.h"
#include "<inc>/OSMap.h"
#include "<inc>/OSAccess.h"
#include "<inc>/OSFile.h"
#include "<inc>/OSPEB.h"
#include "<inc>/OSObject.h"

fn DLLMain { -- }
	// initialize the process image by mapping its sections into the address
	// space and dynamically linking it if necessary.

	// SystemInit.exe is a special case where no image file name is given in
	// the PEB.

	auto ok

	auto imagepath
	OSPEB OSPEBs_ImagePath + imagepath!

	if (imagepath@ gb ~~)
		// this is SystemInit.exe

		OSPEB OSPEBs_ImagePath + // dest
		"/mintia/SystemInit.exe" // src
		strcpy

		-1 OSPEB OSPEBs_StdIn + !
		-1 OSStdIn!

		-1 OSPEB OSPEBs_StdOut + !
		-1 OSStdOut!

		-1 OSPEB OSPEBs_StdErr + !
		-1 OSStdErr!

		// initialize current directory as /

		-1 OSPEB OSPEBs_CurrentDirectory + !
		OSPEB OSPEBs_CurrentDirectoryPath + // dest
		"/" // src
		strcpy
	end

	auto handle

	0 // flags
	ACCESS_READ ACCESS_EXEC | // access
	imagepath@ // path
	OSOpen ok! handle!

	if (ok@)
		// TODO unblock creator thread w/ error code
		ok@ 0 "failed to open" OSSystemAbort drop
	end

	auto sectionhandle

	ACCESS_READ ACCESS_EXEC | // access
	0 // anonsize
	handle@ // filehandle
	ACCESS_OWNER_ALL // permissions
	0 // name
	OSSectionCreate ok! sectionhandle!

	if (ok@)
		// TODO unblock creator thread w/ error code
		ok@ 0 "failed to create section" OSSystemAbort drop
	end

	auto realva

	4096 // length
	0 // startva
	0 // sectionoffset
	sectionhandle@ // sectionhandle
	OSCURRENTPROCESS // processhandle
	PAGEACCESS_READ PAGEACCESS_WRITECOPY | // pageprotection
	0 // flags
	OSSectionMapView ok! realva!

	if (ok@)
		// TODO unblock creator thread w/ error code
		ok@ 0 "failed to map" OSSystemAbort drop
	end

	sectionhandle@ OSClose drop

	4096 // length
	realva@ // vaddr
	OSCURRENTPROCESS // processhandle
	OSUnmapView ok!

	if (ok@)
		ok@ 0 "failed to unmap" OSSystemAbort drop
	end

	while (1) end
end