#include "<df>/dragonfruit.h"

#include "<inc>/OSCalls.h"
#include "<inc>/OSMap.h"
#include "<inc>/OSFile.h"
#include "<inc>/OSPEB.h"
#include "<inc>/OSAbort.h"
#include "<inc>/OSAccess.h"
#include "<inc>/OSObject.h"
#include "<inc>/OSStatus.h"

fn OSFileOpen { flags access path ftype -- handle ok }
	flags@ // flags
	access@ // access
	path@ // path
	OSOpen ok! handle!

	if (ok@)
		return
	end

	auto query

	if (path@ "//:/" 4 strncmp)
		// is in object namespace, might not be a file, need to check.
		// don't need to check otherwise because current directory can only
		// be a literal directory (which won't be containing any non-file objects).

		OSObjectQuery_SIZEOF alloc query!

		handle@ // handle
		query@ // query
		OSObQuery ok!

		if (ok@)
			handle@ OSClose drop

			return
		end

		if (query@ OSObjectQuery_TypeName + @ "File" strcmp ~~)
			handle@ OSClose drop

			STATUS_INVALID_OBJECT_TYPE ok!

			return
		end
	end

	if (ftype@)
		// there's a required file type

		OSFileInformation_SIZEOF alloc query!

		handle@ // handle
		query@ // query
		OSFileQuery ok!

		if (ok@)
			handle@ OSClose drop

			return
		end

		if (query@ OSFileInformation_Type + @ ftype@ ~=)
			handle@ OSClose drop

			STATUS_NOT_CORRECT_FILETYPE ok!

			return
		end
	end
end