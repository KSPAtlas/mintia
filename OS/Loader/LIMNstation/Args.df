#include "<df>/dragonfruit.h"

buffer ArgsBuffer 256
public ArgsBuffer

fn ArgsInit { argp -- }
	auto p
	ArgsBuffer p!

	auto l
	0 l!

	auto esc
	0 esc!

	if (argp@ ~~)
		return
	end

	while (argp@ gb)
		if (l@ 255 >=)
			1 p -=

			while (p@ gb)
				0 p@ sb
				1 p -=
			end

			return
		end

		if (esc@)
			0 esc!
			argp@ gb p@ sb
			1 p +=
			1 l +=
		end elseif (argp@ gb ' ' ==)
			0 p@ sb
			1 p +=
			1 l +=
		end elseif (argp@ gb '\\' ==)
			1 esc!
		end else
			argp@ gb p@ sb
			1 p +=
			1 l +=
		end

		1 argp +=
	end

	0 p@ sb
end

buffer ArgsNameBuffer 32

fn ArgsValue { arg -- out }
	auto p
	ArgsBuffer p!

	auto tp
	ArgsNameBuffer tp!

	auto tpm
	tp@ 32 + tpm!

	auto m
	p@ 255 + m!

	0 out!

	while (p@ m@ <)
		if (p@ gb ~~)
			ArgsNameBuffer tp!

			1 p +=

			if (p@ gb ~~)
				break
			end
		end elseif (p@ gb '=' ==)
			0 tp@ sb

			if (ArgsNameBuffer arg@ strcmp)
				p@ 1 + out!
				return
			end

			p@ strlen p +=

			continue
		end

		p@ gb tp@ sb

		1 tp +=
		1 p +=
	end
end

fn ArgsCheck { arg -- present }
	auto p
	ArgsBuffer p!

	auto m
	p@ 255 + m!

	0 present!

	while (p@ m@ <)
		if (p@ gb ~~)
			break
		end

		if (arg@ p@ strcmp)
			1 present!
			return
		end else
			p@ strlen 1 + p +=
		end
	end
end