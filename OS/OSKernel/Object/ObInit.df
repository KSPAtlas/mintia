#include "<df>/dragonfruit.h"
#include "<inc>/HALLog.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Object.h"

#include "<inc>/Memory.h"

#include "<inc>/Process.h"

var ObTypeObjectType 0
public ObTypeObjectType

var ObTypeDirectoryType 0
public ObTypeDirectoryType

buffer ObRootMutex KeMutex_SIZEOF
public ObRootMutex

fn ObInitPhase0 { -- }
	auto ok

	auto idleproc
	KeProcessCurrent idleproc!

	// give the idle process a handle table

	auto handletable

	idleproc@ // chargedprocess
	OBHANDLEENTRYSIZELOG // entrysizelog
	ExHandleTableCreate ok! handletable!

	if (ok@)
		"ObInitPhase0: couldn't allocate idle process handletable\n" KeCrash
	end

	handletable@ idleproc@ PsProcess_HandleTable + !

	auto type

	auto typeinit
	ObTypeInitializer_SIZEOF alloc typeinit!

	// initialize ObTypeInitializer fields

	ObTypeInitializer_SIZEOF typeinit@ ObTypeInitializer_Length + !
	0 typeinit@ ObTypeInitializer_OpenFunction + !
	0 typeinit@ ObTypeInitializer_CloseFunction + !
	0 typeinit@ ObTypeInitializer_DeleteFunction + !

	// create Type type

	"Type" typeinit@ ObTypeInitializer_Name + !
	'ObTy' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "ObInitPhase0: couldn't create Type type (%i)\n" KeCrash
	end

	if (ObTypeObjectType@ ~~)
		"ObInitPhase0: couldn't create Type type\n" KeCrash
	end

	// create Directory type

	"Directory" typeinit@ ObTypeInitializer_Name + !
	'ObDr' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "ObInitPhase0: couldn't create Directory type (%i)\n" KeCrash
	end

	type@ ObTypeDirectoryType!

	"ObRootMutex" // name
	KERNELMODE // mode
	ObRootMutex // mutex
	KeMutexInitialize ok!

	if (ok@)
		ok@ "ObInitPhase0: couldn't initialize root directory mutex (%i)\n" KeCrash
	end
end

fn ObInitPhase1 { -- }

end

fn (ExHandleEnumFunction) ObHandleDestroyFunction { entryptr handle handletable -- }
	auto process
	handletable@ ExHandleTableHeader_ChargedProcess + @ process!

	if (DEBUGCHECKS)
		if (process@ ~~)
			"ObHandleDestroyFunction: handle table had no process\n" KeCrash
		end
	end

	auto ok

	handle@ process@ ObObjectCloseProcess ok!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ "ObHandleDestroyFunction: failed to close handle (%i)\n" KeCrash
		end
	end
end