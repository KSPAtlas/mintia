#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

#include "<inc>/HALCPU.h"

#include "<inc>/HALDebug.h"

var PsProcessObjectType 0
public PsProcessObjectType

var PsThreadObjectType 0
public PsThreadObjectType

var PsProcessTable 0
public PsProcessTable

var PsProcessListHead 0
public PsProcessListHead

var PsSystemProcess 0
public PsSystemProcess

fn PsInitPhase0 { -- }
	auto ok

	0 // chargedprocess
	0 // entrysizelog
	ExHandleTableCreate ok! PsProcessTable!

	if (ok@)
		"PsInitPhase0: couldn't create Process table\n" KeCrash
	end

	auto type

	auto typeinit
	ObTypeInitializer_SIZEOF alloc typeinit!

	// initialize ObTypeInitializer fields

	ObTypeInitializer_SIZEOF typeinit@ ObTypeInitializer_Length + !
	0 typeinit@ ObTypeInitializer_OpenFunction + !
	0 typeinit@ ObTypeInitializer_CloseFunction + !

	// create Process type

	"Process" typeinit@ ObTypeInitializer_Name + !
	'ObPs' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	pointerof PsProcessObjectDelete typeinit@ ObTypeInitializer_DeleteFunction + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "PsInitPhase0: couldn't create Process type (%i)\n" KeCrash
	end

	type@ PsProcessObjectType!

	// create Thread type

	"Thread" typeinit@ ObTypeInitializer_Name + !
	'ObTh' typeinit@ ObTypeInitializer_Tag + !
	0 typeinit@ ObTypeInitializer_BodySize + !
	pointerof PsThreadObjectDelete typeinit@ ObTypeInitializer_DeleteFunction + !
	typeinit@ ObTypeCreate ok! type!

	if (ok@)
		ok@ "PsInitPhase0: couldn't create Thread type (%i)\n" KeCrash
	end

	type@ PsThreadObjectType!

	auto handle

	ACCESS_OWNER_ALL "OSKernel.exe" PsProcessCreate ok! handle!

	if (ok@)
		ok@ "PsInitPhase0: couldn't create 'OSKernel.exe' process (%i)\n" KeCrash
	end

	PsProcessObjectType@ // type
	handle@ // handle
	ObObjectReferenceByHandle ok! PsSystemProcess! drop

	if (ok@)
		ok@ "PsInitPhase0: couldn't reference system process (%i)\n" KeCrash
	end

	0 // context1
	0 // context2
	pointerof ExInitPhase1 // startfunc
	ACCESS_OWNER_ALL // permissions
	"Main" // name
	0 // processhandle
	PsSystemProcess@ // processptr
	PsThreadCreate ok! handle!

	if (ok@)
		ok@ "PsInitPhase0: couldn't create 'Main' thread (%i)\n" KeCrash
	end

	handle@ ObObjectClose drop

	if (ok@)
		ok@ "PsInitPhase0: couldn't close 'Main' thread (%i)\n" KeCrash
	end

	// this thread will drop back out into ExInit.df, and then become the zero page worker thread
	// in MmZeroPageWorker.df.
end

fn PsInitPhase1 { -- }

end