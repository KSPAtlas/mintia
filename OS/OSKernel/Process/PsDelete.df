#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Security.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OSStatus.h"

#include "<inc>/HALCPU.h"

#include "<inc>/HALDebug.h"

fn (ObTypeDeleteFunction) PsProcessObjectDelete { object -- }
	// the last reference to this process object was removed.
	// finally free all resources associated with it.

	// destroy PID

	// XXX should this be done in the context of the process's last thread?

	auto ok

	object@ PsProcess_PID + @ // handle
	PsProcessTable@ // handletable
	0 // locked
	ExHandleDelete ok! drop

	if (DEBUGCHECKS)
		if (ok@)
			ok@ "PsProcessObjectDelete: failed to delete PID (%i)\n" KeCrash
		end
	end

	// destroy handle table

	pointerof ObHandleDestroyFunction // enum func
	object@ PsProcess_HandleTable + @ // handle table
	ExHandleTableDelete

	// remove from the global process list

	auto rs
	HALCPUInterruptDisable rs!

	auto n
	object@ PsProcess_GlobalListNext + @ n!

	auto l
	object@ PsProcess_GlobalListPrev + @ l!

	if (n@)
		l@ n@ PsProcess_GlobalListPrev + !
	end

	if (l@)
		n@ l@ PsProcess_GlobalListNext + !
	end else
		// no prev means we were the listhead

		n@ PsProcessListHead!
	end

	rs@ HALCPUInterruptRestore

	// deref quota block

	object@ PsProcess_QuotaBlock + @ MmQuotaBlockDereference

	// uninitialize virtual memory structures

	object@ MmProcessUninitialize ok!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ "PsProcessObjectDelete: failed to uninitialize virtual memory (%i)\n" KeCrash
		end
	end
end

fn (ObTypeDeleteFunction) PsThreadObjectDelete { object -- }
	// the last reference to this thread object was removed.
	// finally free all resources associated with it.

	"deleting thread\n" Printf

	if (DEBUGCHECKS)
		if (object@ KeThread_Enqueued + @)
			"PsThreadObjectDelete: attempt to delete enqueued thread\n" KeCrash
		end

		if (object@ KeThread_Status + @ THREADSTATUS_TERMINATED ~=)
			"PsThreadObjectDelete: attempt to delete non-terminated thread\n" KeCrash
		end
	end

	object@ KeThread_KernelStackBottom + @ MmKernelStackFree

	object@ KeThread_Process + @ ObObjectDereferenceByPointer drop

	object@ KeThreadUninitialize
end