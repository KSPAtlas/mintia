#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALRTC.h"

#include "<inc>/Kernel.h"

#include "<ll>/OSDLL/OSStatus.h"

// initializes a KeMutex

fn KeMutexInitialize { name mode mutex -- ok }
	name@ DISPATCHOBJECT_MUTEX mutex@ KeDispatchInitialize ok!

	if (ok@)
		return
	end

	0 mutex@ KeMutex_OwningThread + !
	0 mutex@ KeMutex_Locked + !
	mode@ mutex@ KeMutex_Mode + !

	0 mutex@ KeMutex_Next + !

	1 mutex@ KeMutex_DispatchHeader + KeDispatchHeader_Signaled + !
end

fn KeMutexOwnerSet { thread mutex -- ok }
	0 ok!

	thread@ mutex@ KeMutex_OwningThread + !
	1 mutex@ KeMutex_Locked + !

	if (mutex@ KeMutex_Mode + @ KERNELMODE ==)
		1 thread@ KeThread_KernelAPCDisableCount + +=
	end

	auto h
	thread@ KeThread_MutexListHead + @ mutex@ KeMutex_Next + !

	mutex@ thread@ KeThread_MutexListHead + !

	0 mutex@ KeMutex_DispatchHeader + KeDispatchHeader_Signaled + !
end

fn KeMutexRelease { mutex -- ok }
	0 ok!

	// if kernelmode mutex, there is NO possible return value except for 0.
	// so, when unlocking a kernelmode mutex, its safe to drop the return value.

	auto current
	KeThreadCurrent@ current!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto lock
	mutex@ KeMutex_Locked + @ lock!

	if (DEBUGCHECKS)
		if (KeDPCActive@)
			"KeMutexRelease: DPC active\n" KeCrash
		end

		if (mutex@ KeMutex_OwningThread + @ current@ ~=)
			// we didn't own it...

			mutex@ KeMutex_DispatchHeader + KeDispatchHeader_Name + @
			current@ KeThread_Name +
			"KeMutexRelease: thread '%s' didn't own mutex '%s'\n" KeCrash
		end

		if (lock@ ~~)
			// it wasn't locked...

			mutex@ KeMutex_DispatchHeader + KeDispatchHeader_Name + @
			current@ KeThread_Name +
			"KeMutexRelease: thread '%s' owns mutex '%s' but lockcount=0\n" KeCrash
		end
	end

	1 lock -=

	if (lock@ ~~)
		// just unlocked it, give it away if necessary

		auto h
		current@ KeThread_MutexListHead + @ h!

		if (h@ mutex@ ~=)
			// attempting to release mutex in wrong order

			if (mutex@ KeMutex_Mode + @ KERNELMODE ==)
				// its a kernel mode mutex! thats a system software bug!

				mutex@ KeMutex_DispatchHeader + KeDispatchHeader_Name + @
				current@ KeThread_Name +
				"KeMutexRelease: thread '%s' mutex '%s': deadlock possible\n" KeCrash
			end

			// its a usermode mutex, we can just return with an error

			ipl@ KeIPLLower

			STATUS_DEADLOCK_POSSIBLE ok!

			return	
		end

		mutex@ KeMutex_Next + @ current@ KeThread_MutexListHead + !

		0 mutex@ KeMutex_Next + !

		auto wokenthread

		if (mutex@ KeMutex_Mode + @ KERNELMODE ==)
			if (current@ KeThread_KernelAPCDisableCount + @ ~~)
				mutex@ KeMutex_DispatchHeader + KeDispatchHeader_Name + @
				current@ KeThread_Name +
				"KeMutexRelease: thread '%s' mutex '%s': APCDisableCount<0\n" KeCrash
			end

			1 current@ KeThread_KernelAPCDisableCount + -=

			// kernel mode mutex, boost waiter priority by 1

			0 // waitstatus
			1 // priboost
			mutex@ // object
			1 // signal
			KeDispatchSatisfyFirst ok! wokenthread!
		end else
			0 // waitstatus
			1 // priboost
			mutex@ // object
			1 // signal
			KeDispatchSatisfyFirst ok! wokenthread!
		end

		if (DEBUGCHECKS)
			if (ok@)
				"KeMutexRelease: couldn't KeDispatchSatisfyFirst\n" KeCrash
			end
		end

		if (wokenthread@)
			// we woke up a thread who was waiting. make him the owner of the mutex

			if (wokenthread@ mutex@ KeMutexOwnerSet)
				"KeMutexRelease: couldn't KeMutexOwnerSet\n" KeCrash
			end
		end else
			// just unown the mutex

			0 mutex@ KeMutex_OwningThread + !
			0 mutex@ KeMutex_Locked + !
		end
	end else
		lock@ mutex@ KeMutex_Locked + !
	end

	ipl@ KeIPLLower
end

fn KeMutexIsLocked { mutex -- locked }
	mutex@ KeMutex_Locked + @ locked!
end