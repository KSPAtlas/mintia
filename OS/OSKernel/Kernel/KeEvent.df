#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALRTC.h"

#include "<inc>/Kernel.h"

#include "<ll>/OSDLL/OSStatus.h"

fn KeEventInitialize { signaled type name event -- ok }
	0 ok!

	if (type@ KEEVENT_SYNCH ==)
		DISPATCHOBJECT_EVENTSYNCH type!		
	end elseif (type@ KEEVENT_NOTIF ==)
		DISPATCHOBJECT_EVENTNOTIF type!
	end else
		type@ "KeEventInitialize: invalid type %d\n" KeCrash
	end

	name@ type@ event@ KeDispatchInitialize ok!

	if (ok@)
		return
	end

	signaled@ event@ KeDispatchHeader_Signaled + !
end

fn KeEventReset { event -- signaled }
	event@ KeDispatchHeader_Signaled + @ signaled!
	0 event@ KeDispatchHeader_Signaled + !
end

fn KeEventSignal { priboost event -- ok }
	0 ok!

	if (event@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTNOTIF ==)
		0 // waitstatus
		priboost@ // priboost
		event@ // object
		KeDispatchSatisfyAll ok! drop
	end elseif (event@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ==)
		auto wth

		0 // waitstatus
		priboost@ // priboost
		event@ // object
		1 // signal
		KeDispatchSatisfyFirst ok! wth!
	end else
		event@ KeDispatchHeader_Type + @ "KeEventSignal: invalid dispatch type %d\n" KeCrash
	end
end

fn KeEventPulse { priboost event -- ok }
	0 ok!

	if (event@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTNOTIF ==)
		auto ipl
		IPLDPC KeIPLRaise ipl!

		0 // waitstatus
		priboost@ // priboost
		event@ // object
		KeDispatchSatisfyAll ok! drop

		0 event@ KeDispatchHeader_Signaled + !

		ipl@ KeIPLLower
	end elseif (event@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ==)
		0 // waitstatus
		priboost@ // priboost
		event@ // object
		0 // signal
		KeDispatchSatisfyFirst ok! drop
	end else
		event@ KeDispatchHeader_Type + @ "KeEventSignal: invalid dispatch type %d\n" KeCrash
	end
end

fn KeEventSignalGetWaiter { priboost event -- waiter ok }
	if (DEBUGCHECKS)
		if (event@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ~=)
			"KeEventSignalGetWaiter: event wasn't a synch event\n" KeCrash
		end
	end

	0 // waitstatus
	priboost@ // priboost
	event@ // object
	1 // signal
	KeDispatchSatisfyFirst ok! waiter!
end