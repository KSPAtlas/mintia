#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALRTC.h"

#include "<inc>/Kernel.h"

#include "<ll>/OSDLL/OSStatus.h"

// initializes a dispatch object's header

fn KeDispatchInitialize { name type dobject -- ok }
	0 ok!

	0 dobject@ KeDispatchHeader_Signaled + !

	type@ dobject@ KeDispatchHeader_Type + !

	name@ dobject@ KeDispatchHeader_Name + !

	0 dobject@ KeDispatchHeader_WaitBlockListHead + !
	0 dobject@ KeDispatchHeader_WaitBlockListTail + !
end

// initializes a WaitBlock

fn KeDispatchWaitBlockInitialize { thread waitblock -- ok }
	0 ok!

	0 waitblock@ KeDispatchWaitBlock_Prev + !
	0 waitblock@ KeDispatchWaitBlock_Next + !

	0 waitblock@ KeDispatchWaitBlock_WaitStatusReturn + !
	0 waitblock@ KeDispatchWaitBlock_WaitType + !

	thread@ waitblock@ KeDispatchWaitBlock_Thread + !

	0 waitblock@ KeDispatchWaitBlock_Object + !
end

fn KeDispatchWaitBlockDequeue { waitstatus waitblock -- ok }
	0 ok!

	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeDispatchWaitBlockDequeue: ipl != IPLDPC\n" KeCrash
		end

		if (waitblock@ KeDispatchWaitBlock_Object + @ ~~)
			"KeDispatchWaitBlockDequeue: attempt to dequeue a non-enqueued WaitBlock\n" KeCrash
		end
	end

	auto object
	waitblock@ KeDispatchWaitBlock_Object + @ object!

	auto n
	waitblock@ KeDispatchWaitBlock_Next + @ n!

	if (n@)
		waitblock@ KeDispatchWaitBlock_Prev + @ n@ KeDispatchWaitBlock_Prev + !
	end else // no next means we were the tail
		waitblock@ KeDispatchWaitBlock_Prev + @ object@ KeDispatchHeader_WaitBlockListTail + !
	end

	auto p
	waitblock@ KeDispatchWaitBlock_Prev + @ p!

	if (p@)
		waitblock@ KeDispatchWaitBlock_Next + @ p@ KeDispatchWaitBlock_Next + !
	end else // no prev means we were the head
		waitblock@ KeDispatchWaitBlock_Next + @ object@ KeDispatchHeader_WaitBlockListHead + !
	end

	0 waitblock@ KeDispatchWaitBlock_Next + !
	0 waitblock@ KeDispatchWaitBlock_Prev + !

	0 waitblock@ KeDispatchWaitBlock_Object + !

	waitstatus@ waitblock@ KeDispatchWaitBlock_WaitStatusReturn + !
end

// adds a WaitBlock to the tail of a dispatch object's WaitBlockList

fn KeDispatchWaitBlockEnqueue { waittype waitblock object -- ok }
	0 ok!

	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeDispatchWaitBlockInsert: ipl != IPLDPC\n" KeCrash
		end

		if (waitblock@ KeDispatchWaitBlock_Object + @)
			"KeDispatchWaitBlockEnqueue: attempt to queue an enqueued WaitBlock\n" KeCrash
		end
	end

	auto t
	object@ KeDispatchHeader_WaitBlockListTail + @ t!

	if (t@ ~~)
		waitblock@ object@ KeDispatchHeader_WaitBlockListHead + !
	end else
		waitblock@ t@ KeDispatchWaitBlock_Next + !
		t@ waitblock@ KeDispatchWaitBlock_Prev + !
	end

	waitblock@ object@ KeDispatchHeader_WaitBlockListTail + !

	object@ waitblock@ KeDispatchWaitBlock_Object + !

	waittype@ waitblock@ KeDispatchWaitBlock_WaitType + !
end

// satisfies a WaitBlock

fn KeDispatchWaitBlockSatisfy { waitstatus priboost waitblock -- ok }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeDispatchWaitBlockSatisfy: ipl != IPLDPC\n" KeCrash
		end
	end

	waitstatus@ waitblock@ KeDispatchWaitBlockDequeue ok!

	if (ok@)
		return
	end

	auto thread
	waitblock@ KeDispatchWaitBlock_Thread + @ thread!

	1 thread@ KeThread_WaitCount + -=

	auto wt
	waitblock@ KeDispatchWaitBlock_WaitType + @ wt!

	if (wt@ THREADWAITANY ==)
		waitstatus@ priboost@ thread@ KeThreadWakeup drop
	end elseif (wt@ THREADWAITALL ==)
		if (thread@ KeThread_WaitCount + @ 0 ==)
			0 priboost@ thread@ KeThreadWakeup drop
		end
	end
end

// satisfies all waiters on a dispatch object
// this SIGNALS the object

fn KeDispatchSatisfyAll { waitstatus priboost object -- count ok }
	0 ok!

	0 count!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	1 object@ KeDispatchHeader_Signaled + !

	auto h
	object@ KeDispatchHeader_WaitBlockListHead + @ h!

	auto wb

	while (h@)
		h@ wb!
		h@ KeDispatchWaitBlock_Next + @ h!

		if (waitstatus@ priboost@ wb@ KeDispatchWaitBlockSatisfy)
			"KeDispatchSatisfyAll: couldn't satisfy WaitBlock\n" KeCrash
		end

		1 count +=
	end

	ipl@ KeIPLLower
end

// satisfies the highest priority, earliest wait on a dispatch object
// this DOES NOT SIGNAL the object

fn KeDispatchSatisfyFirst { waitstatus priboost object -- wokenthread ok }
	0 ok!

	0 wokenthread!

	auto wthpri
	0 wthpri!

	auto wokewb
	0 wokewb!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto h
	object@ KeDispatchHeader_WaitBlockListHead + @ h!

	while (h@)
		auto nw
		h@ KeDispatchWaitBlock_Thread + @ nw!

		auto npri

		if (wokenthread@ ~~)
			h@ wokewb!

			nw@ wokenthread!

			nw@ KeThread_BasePriority + @ wthpri!
		end else
			nw@ KeThread_BasePriority + @ npri!

			if (npri@ wthpri@ >)
				h@ wokewb!

				nw@ wokenthread!

				npri@ wthpri!
			end
		end

		h@ KeDispatchWaitBlock_Next + @ h!
	end

	if (wokewb@)
		if (waitstatus@ priboost@ wokewb@ KeDispatchWaitBlockSatisfy)
			"KeDispatchSatisfyFirst: couldn't satisfy WaitBlock\n" KeCrash
		end
	end

	ipl@ KeIPLLower
end

fn KeThreadWaitForMultipleObjects { waitmode alertable waittype timeout objectcount objecttable -- ok }
	0 ok!

	if (objectcount@ 0 ==)
		return
	end

	if (objectcount@ THREADWAITBLOCKS >)
		"KeThreadWaitForMultipleObjects: objectcount>THREADWAITBLOCKS\n" KeCrash
	end

	auto current
	KeThreadCurrent@ current!

	auto waitipl
	IPLDPC KeIPLRaise waitipl!

	// XXX timeout will reset if interrupted by an APC

	// XXX mutexes don't lock properly if you use THREADWAITALL

	while (1)
		auto i
		0 i!

		auto waitblock
		current@ KeThread_WaitBlocks + waitblock! // now points to current thread's first waitblock

		auto satisfied

		if (waittype@ THREADWAITANY ==)
			0 satisfied!
		end else
			1 satisfied!
		end

		objectcount@ current@ KeThread_WaitCount + !

		while (i@ objectcount@ <)
			auto object
			objecttable@ i@ 2 << + @ object!

			if (waittype@ THREADWAITANY ==)
				if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_MUTEX ==)
					if (object@ KeMutex_OwningThread + @ current@ ==)
						1 object@ KeMutex_Locked + +=

						i@ ok!
						1 satisfied!
						break
					end elseif (object@ KeDispatchHeader_Signaled + @)
						if (current@ object@ KeMutexOwnerSet)
							"KeThreadWaitForMultipleObjects: couldn't KeMutexOwnerSet\n" KeCrash
						end

						i@ ok!
						1 satisfied!
						break
					end
				end else
					if (object@ KeDispatchHeader_Signaled + @)
						if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ==)
							0 object@ KeDispatchHeader_Signaled + !
						end

						i@ ok!
						1 satisfied!
						break
					end
				end
			end else
				if (object@ KeDispatchHeader_Signaled + @ ~~)
					if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_MUTEX ==)
						if (object@ KeMutex_OwningThread + @ current@ ~=)
							0 satisfied!
						end
					end else
						0 satisfied!
					end
				end elseif (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ==)
					0 object@ KeDispatchHeader_Signaled + !
				end
			end

			if (timeout@)
				if (waittype@ waitblock@ object@ KeDispatchWaitBlockEnqueue)
					"KeThreadWaitForMultipleObjects: couldn't enqueue WaitBlock\n" KeCrash
				end
			end

			KeDispatchWaitBlock_SIZEOF waitblock +=
			1 i +=
		end

		if (timeout@ ~~)
			// no timeout. just return whether the wait is satisfied or not

			satisfied@ ok!

			break
		end elseif (satisfied@)
			// the wait is already satisfied. dequeue any wait blocks we just enqueued
			// and then return.

			if (current@ KeThreadWaitBlocksDequeue)
				"KeThreadWaitForMultipleObjects: couldn't dequeue all WaitBlocks\n" KeCrash
			end

			break
		end

		if (timeout@ TIMEOUTINFINITE ~=)
			// there's a timeout and its not infinite, set up a timer waitblock

			if ("WaitTimeout" 0 current@ KeThread_Timer + KeTimerInitialize)
				"KeThreadWaitForMultipleObjects: couldn't initialize timeout timer\n" KeCrash
			end

			current@ KeThread_TimeoutWaitBlock + waitblock!

			if (THREADWAITANY waitblock@ current@ KeThread_Timer + KeDispatchWaitBlockEnqueue)
				"KeThreadWaitForMultipleObjects: couldn't initialize timeout WaitBlock\n" KeCrash
			end

			if (0 0 timeout@ current@ KeThread_Timer + KeTimerEnqueue)
				"KeThreadWaitForMultipleObjects: couldn't enqueue timeout timer\n" KeCrash
			end
		end

		waitipl@ waitmode@ alertable@ KeThreadWait ok!

		// if we got woken up to deliver an APC, it will be executed here when we
		// lower the IPL.
		// the object may become signaled while this occurs, hence the check above.

		if (waitipl@ IPLLOW ==)
			IPLLOW KeIPLLower

			// we were awoken to deliver a kernel APC. raise back to IPLDPC and retry.

			IPLDPC KeIPLRaise drop
		end

		if (ok@ STATUS_KERNEL_APC ~=)
			break
		end
	end

	waitipl@ KeIPLLower
end

fn KeThreadWaitForObject { waitmode alertable timeout object -- ok }
	auto objecttable
	4 alloc objecttable!

	object@ objecttable@!

	waitmode@ alertable@ THREADWAITANY timeout@ 1 objecttable@ KeThreadWaitForMultipleObjects ok!
end

fn KeThreadWakeup { waitstatus priboost thread -- woken }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeThreadWakeup: ipl != IPLDPC\n" KeCrash
		end
	end

	0 woken!

	auto status
	thread@ KeThread_Status + @ status!

	if (status@ THREADSTATUS_TERMINATED ==)
		// XXX are there legitimate, normal situations where you might try to wake up a dead thread
		// that has not yet been cleaned up?

		"KeThreadWakeup: thread is already dead\n" KeCrash
	end

	if (status@ THREADSTATUS_WAITINGALERTABLE ~= status@ THREADSTATUS_WAITINGUNALERTABLE ~= &&)
		// thread isn't waiting

		return
	end

	if (thread@ KeThreadWaitBlocksDequeue)
		"KeThreadWakeup: couldn't dequeue all WaitBlocks\n" KeCrash
	end

	if (thread@ KeThread_Timer + KeTimer_Enqueued + @)
		if (thread@ KeThread_Timer + KeTimerDequeue)
			"KeThreadWakeup: couldn't dequeue thread timer\n" KeCrash
		end
	end

	priboost@ thread@ KeThreadPriorityBoost
	waitstatus@ thread@ KeThread_WaitStatus + !
	QUEUEBACK thread@ KeThreadReady

	1 woken!
end

fn KeThreadWaitBlocksDequeue { thread -- ok }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeThreadWaitBlocksDequeue: ipl != IPLDPC\n" KeCrash
		end
	end

	0 ok!

	auto wb
	thread@ KeThread_WaitBlocks + wb!

	auto i
	0 i!

	while (i@ THREADWAITBLOCKS <)
		if (wb@ KeDispatchWaitBlock_Object + @)
			if (0 wb@ KeDispatchWaitBlockDequeue)
				"KeThreadWaitBlocksDequeue: couldn't dequeue WaitBlock\n" KeCrash
			end
		end

		1 i +=
		KeDispatchWaitBlock_SIZEOF wb +=
	end

	thread@ KeThread_TimeoutWaitBlock + wb!

	if (wb@ KeDispatchWaitBlock_Object + @)
		if (0 wb@ KeDispatchWaitBlockDequeue)
			"KeThreadWaitBlocksDequeue: couldn't dequeue timeout WaitBlock\n" KeCrash
		end
	end
end

fn KeThreadWait { waitipl waitmode alertable -- ok }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeThreadWait: ipl != IPLDPC\n" KeCrash
		end

		if (waitipl@ IPLDPC >=)
			"KeThreadWait: waitipl >= IPLDPC\n" KeCrash
		end
	end

	auto current
	KeThreadCurrent@ current!

	waitmode@ current@ KeThread_WaitMode + !

	waitipl@ current@ KeThread_WaitIPL + !

	0 current@ KeThread_WaitStatus + !

	if (alertable@)
		THREADSTATUS_WAITINGALERTABLE KeThreadYield
	end else
		THREADSTATUS_WAITINGUNALERTABLE KeThreadYield
	end

	current@ KeThread_WaitStatus + @ ok!
end

fn KeThreadSleep { ms waitmode alertable -- ok }
	0 ok!	

	if (ms@ 0 ==)
		THREADSTATUS_READY KeThreadYield
	end

	auto current
	KeThreadCurrent@ current!

	"KeThreadSleep" 0 current@ KeThread_Timer + KeTimerInitialize ok!

	if (DEBUGCHECKS)
		if (ok@)
			"KeThreadSleep: couldn't initialize timer\n" KeCrash
		end
	end

	if (0 0 ms@ current@ KeThread_Timer + KeTimerEnqueue)
		"KeThreadSleep: couldn't enqueue timer\n" KeCrash
	end

	waitmode@ alertable@ TIMEOUTINFINITE current@ KeThread_Timer + KeThreadWaitForObject ok!
end