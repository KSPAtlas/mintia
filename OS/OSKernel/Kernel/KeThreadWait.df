#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALRTC.h"

#include "<inc>/Kernel.h"

#include "<ll>/OSDLL/OS.h"

// initializes a dispatch object's header

fn KeDispatchInitialize { name type dobject -- ok }
	0 ok!

	0 dobject@ KeDispatchHeader_Signaled + !

	type@ dobject@ KeDispatchHeader_Type + !

	name@ dobject@ KeDispatchHeader_Name + !

	0 dobject@ KeDispatchHeader_WaitBlockListHead + !
	0 dobject@ KeDispatchHeader_WaitBlockListTail + !
end

// initializes a WaitBlock

fn KeDispatchWaitBlockInitialize { thread waitblock -- ok }
	0 ok!

	0 waitblock@ KeDispatchWaitBlock_Prev + !
	0 waitblock@ KeDispatchWaitBlock_Next + !

	0 waitblock@ KeDispatchWaitBlock_WaitStatusReturn + !
	0 waitblock@ KeDispatchWaitBlock_WaitType + !

	thread@ waitblock@ KeDispatchWaitBlock_Thread + !

	0 waitblock@ KeDispatchWaitBlock_Object + !
end

fn KeDispatchWaitBlockDequeue { waitstatus waitblock -- ok }
	0 ok!

	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeDispatchWaitBlockDequeue: ipl != IPLDPC\n" KeCrash
		end

		if (waitblock@ KeDispatchWaitBlock_Object + @ ~~)
			"KeDispatchWaitBlockDequeue: attempt to dequeue a non-enqueued WaitBlock\n" KeCrash
		end
	end

	auto object
	waitblock@ KeDispatchWaitBlock_Object + @ object!

	auto n
	waitblock@ KeDispatchWaitBlock_Next + @ n!

	if (n@)
		waitblock@ KeDispatchWaitBlock_Prev + @ n@ KeDispatchWaitBlock_Prev + !
	end else // no next means we were the tail
		waitblock@ KeDispatchWaitBlock_Prev + @ object@ KeDispatchHeader_WaitBlockListTail + !
	end

	auto p
	waitblock@ KeDispatchWaitBlock_Prev + @ p!

	if (p@)
		waitblock@ KeDispatchWaitBlock_Next + @ p@ KeDispatchWaitBlock_Next + !
	end else // no prev means we were the head
		waitblock@ KeDispatchWaitBlock_Next + @ object@ KeDispatchHeader_WaitBlockListHead + !
	end

	0 waitblock@ KeDispatchWaitBlock_Next + !
	0 waitblock@ KeDispatchWaitBlock_Prev + !

	0 waitblock@ KeDispatchWaitBlock_Object + !

	waitstatus@ waitblock@ KeDispatchWaitBlock_WaitStatusReturn + !
end

// adds a WaitBlock to the tail of a dispatch object's WaitBlockList

fn KeDispatchWaitBlockEnqueue { waittype waitblock object -- ok }
	0 ok!

	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeDispatchWaitBlockInsert: ipl != IPLDPC\n" KeCrash
		end

		if (waitblock@ KeDispatchWaitBlock_Object + @)
			"KeDispatchWaitBlockEnqueue: attempt to queue an enqueued WaitBlock\n" KeCrash
		end
	end

	auto t
	object@ KeDispatchHeader_WaitBlockListTail + @ t!

	if (t@ ~~)
		waitblock@ object@ KeDispatchHeader_WaitBlockListHead + !
	end else
		waitblock@ t@ KeDispatchWaitBlock_Next + !
		t@ waitblock@ KeDispatchWaitBlock_Prev + !
	end

	waitblock@ object@ KeDispatchHeader_WaitBlockListTail + !

	object@ waitblock@ KeDispatchWaitBlock_Object + !

	waittype@ waitblock@ KeDispatchWaitBlock_WaitType + !
end

// satisfies a WaitBlock

fn KeDispatchWaitBlockSatisfy { waitstatus priboost waitblock -- ok }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeDispatchWaitBlockSatisfy: ipl != IPLDPC\n" KeCrash
		end
	end

	waitstatus@ waitblock@ KeDispatchWaitBlockDequeue ok!

	if (ok@)
		return
	end

	auto thread
	waitblock@ KeDispatchWaitBlock_Thread + @ thread!

	1 thread@ KeThread_WaitCount + -=

	auto wt
	waitblock@ KeDispatchWaitBlock_WaitType + @ wt!

	if (wt@ THREADWAITANY ==)
		waitstatus@ priboost@ thread@ KeThreadWakeup drop
	end elseif (wt@ THREADWAITALL ==)
		if (thread@ KeThread_WaitCount + @ 0 ==)
			0 priboost@ thread@ KeThreadWakeup drop
		end
	end
end

// satisfies all waiters on a dispatch object
// this SIGNALS the object

fn KeDispatchSatisfyAll { waitstatus priboost object -- count ok }
	0 ok!

	0 count!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	1 object@ KeDispatchHeader_Signaled + !

	auto h
	object@ KeDispatchHeader_WaitBlockListHead + @ h!

	auto wb

	while (h@)
		h@ wb!
		h@ KeDispatchWaitBlock_Next + @ h!

		waitstatus@ priboost@ wb@ KeDispatchWaitBlockSatisfy ok!
		
		if (DEBUGCHECKS)
			if (ok@)
				"KeDispatchSatisfyAll: couldn't satisfy WaitBlock\n" KeCrash
			end
		end

		1 count +=
	end

	ipl@ KeIPLLower
end

// satisfies the earliest wait on a dispatch object.
// this signals the object if no thread was awoken and signal is 1.

// XXX maybe this should wake up the *highest priority* earliest waiter?
// that would require keeping the waitblock list insertion sorted to do
// efficiently which I don't feel like working on right now.

fn KeDispatchSatisfyFirst { waitstatus priboost object signal -- wokenthread ok }
	0 ok!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto h
	object@ KeDispatchHeader_WaitBlockListHead + @ h!

	if (h@)
		h@ KeDispatchWaitBlock_Thread + @ wokenthread!
		waitstatus@ priboost@ h@ KeDispatchWaitBlockSatisfy ok!

		if (DEBUGCHECKS)
			if (ok@)
				"KeDispatchSatisfyFirst: couldn't satisfy WaitBlock\n" KeCrash
			end
		end
	end elseif (signal@)
		0 wokenthread!
		1 object@ KeDispatchHeader_Signaled + !
	end

	ipl@ KeIPLLower
end

fn KeThreadWaitForMultipleObjects { waitmode alertable waittype timeout objectcount objecttable -- ok }
	0 ok!

	if (objectcount@ 0 ==)
		return
	end

	if (objectcount@ THREADWAITBLOCKS >)
		"KeThreadWaitForMultipleObjects: objectcount>THREADWAITBLOCKS\n" KeCrash
	end

	if (waittype@ THREADWAITALL ==)
		// THREADWAITALL is broken and requires a ~1 afternoon dispatcher
		// overhaul to make work because I am bad. THREADWAITANY works fine,
		// as far as I can tell.

		STATUS_NOT_IMPLEMENTED ok!

		return
	end

	auto current
	KeThreadCurrent@ current!

	auto waitipl
	IPLDPC KeIPLRaise waitipl!

	waitmode@ // mode
	alertable@ // alertable
	current@ // thread
	KeThreadIsKilled ok!

	if (ok@)
		// thread was killed or received a signal that cannot be ignored
		waitipl@ KeIPLLower

		return
	end

	// XXX timeout will reset if interrupted by an APC

	// XXX mutexes don't lock properly if you use THREADWAITALL

	while (1)
		auto i
		0 i!

		auto waitblock
		current@ KeThread_WaitBlocks + waitblock! // now points to current thread's first waitblock

		auto satisfied

		if (waittype@ THREADWAITANY ==)
			0 satisfied!
		end else
			1 satisfied!
		end

		objectcount@ current@ KeThread_WaitCount + !

		while (i@ objectcount@ <)
			auto object
			objecttable@ i@ 2 << + @ object!

			if (waittype@ THREADWAITANY ==)
				if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_MUTEX ==)
					if (object@ KeMutex_OwningThread + @ current@ ==)
						1 object@ KeMutex_Locked + +=

						i@ ok!
						1 satisfied!
						break
					end elseif (object@ KeDispatchHeader_Signaled + @)
						current@ object@ KeMutexOwnerSet ok!

						if (DEBUGCHECKS)
							if (ok@)
								"KeThreadWaitForMultipleObjects: couldn't KeMutexOwnerSet\n" KeCrash
							end
						end

						i@ ok!
						1 satisfied!
						break
					end
				end else
					if (object@ KeDispatchHeader_Signaled + @)
						if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ==)
							0 object@ KeDispatchHeader_Signaled + !
						end elseif (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_SEMAPHORE ==)
							if (DEBUGCHECKS)
								if (object@ KeSemaphore_Count + @ 0 ==)
									"KeThreadWaitForMultipleObjects: semaphore count==0\n" KeCrash
								end
							end

							1 object@ KeSemaphore_Count + -=

							if (object@ KeSemaphore_Count + @ 0 ==)
								0 object@ KeDispatchHeader_Signaled + !
							end
						end

						i@ ok!
						1 satisfied!
						break
					end
				end
			end else
				if (object@ KeDispatchHeader_Signaled + @ ~~)
					if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_MUTEX ==)
						if (object@ KeMutex_OwningThread + @ current@ ~=)
							0 satisfied!
						end
					end else
						0 satisfied!
					end
				end elseif (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_EVENTSYNCH ==)
					0 object@ KeDispatchHeader_Signaled + !
				end
			end

			if (timeout@)
				waittype@ waitblock@ object@ KeDispatchWaitBlockEnqueue ok!

				if (DEBUGCHECKS)
					if (ok@)
						"KeThreadWaitForMultipleObjects: couldn't enqueue WaitBlock\n" KeCrash
					end
				end
			end

			KeDispatchWaitBlock_SIZEOF waitblock +=
			1 i +=
		end

		if (timeout@ ~~)
			// no timeout. just return whether the wait is satisfied or not

			satisfied@ ok!

			break
		end elseif (satisfied@)
			// the wait is already satisfied. dequeue any wait blocks we just enqueued
			// and then return.

			current@ KeThreadWaitBlocksDequeue ok!

			if (DEBUGCHECKS)
				if (ok@)
					"KeThreadWaitForMultipleObjects: couldn't dequeue all WaitBlocks\n" KeCrash
				end
			end

			break
		end

		if (timeout@ OSWAIT_TIMEOUTINFINITE ~=)
			// there's a timeout and its not infinite, set up a timer waitblock

			"WaitTimeout" 0 current@ KeThread_Timer + KeTimerInitialize ok!

			if (DEBUGCHECKS)
				if (ok@)
					"KeThreadWaitForMultipleObjects: couldn't initialize timeout timer\n" KeCrash
				end
			end

			current@ KeThread_TimeoutWaitBlock + waitblock!

			THREADWAITANY waitblock@ current@ KeThread_Timer + KeDispatchWaitBlockEnqueue ok!

			if (DEBUGCHECKS)
				if (ok@)
					"KeThreadWaitForMultipleObjects: couldn't initialize timeout WaitBlock\n" KeCrash
				end
			end

			0 0 timeout@ current@ KeThread_Timer + KeTimerEnqueue ok!

			if (DEBUGCHECKS)
				if (ok@)
					"KeThreadWaitForMultipleObjects: couldn't enqueue timeout timer\n" KeCrash
				end
			end
		end

		waitipl@ waitmode@ alertable@ KeThreadWait ok!

		// if we got woken up to deliver an APC, it will be executed here when we
		// lower the IPL.
		// the object may become signaled while this occurs, hence the check above.

		if (ok@ STATUS_KERNEL_APC ==)
			if (DEBUGCHECKS)
				if (waitipl@ IPLAPC ==)
					"KeThreadWaitForMultipleObjects: awoken for APC while waiting at IPLAPC\n" KeCrash
				end
			end

			IPLLOW KeIPLLower

			// we were awoken to deliver a kernel APC. raise back to IPLDPC and retry.

			IPLDPC KeIPLRaise drop
		end else
			break
		end
	end

	waitipl@ KeIPLLower
end

fn KeThreadWaitForObject { waitmode alertable timeout object -- ok }
	auto objecttable
	4 alloc objecttable!

	object@ objecttable@!

	waitmode@ alertable@ THREADWAITANY timeout@ 1 objecttable@ KeThreadWaitForMultipleObjects ok!
end

fn KeThreadWakeup { waitstatus priboost thread -- woken }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeThreadWakeup: ipl != IPLDPC\n" KeCrash
		end
	end

	0 woken!

	auto status
	thread@ KeThread_Status + @ status!

	if (status@ THREADSTATUS_TERMINATED ==)
		// XXX are there legitimate, normal situations where you might try to wake up a dead thread
		// that has not yet been cleaned up?

		"KeThreadWakeup: thread is already dead\n" KeCrash
	end

	if (status@ THREADSTATUS_WAITINGALERTABLE ~= status@ THREADSTATUS_WAITINGUNALERTABLE ~= &&)
		// thread isn't waiting

		return
	end

	thread@ KeThreadWaitBlocksDequeue woken!

	if (DEBUGCHECKS)
		if (woken@)
			"KeThreadWakeup: couldn't dequeue all WaitBlocks\n" KeCrash
		end
	end

	if (thread@ KeThread_Timer + KeTimer_Enqueued + @)
		thread@ KeThread_Timer + KeTimerDequeue woken!

		if (DEBUGCHECKS)
			if (woken@)
				"KeThreadWakeup: couldn't dequeue thread timer\n" KeCrash
			end
		end
	end

	priboost@ thread@ KeThreadPriorityBoost
	waitstatus@ thread@ KeThread_WaitStatus + !
	QUEUEBACK thread@ KeThreadReady

	1 woken!
end

fn KeThreadWaitBlocksDequeue { thread -- ok }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeThreadWaitBlocksDequeue: ipl != IPLDPC\n" KeCrash
		end
	end

	0 ok!

	auto wb
	thread@ KeThread_WaitBlocks + wb!

	auto i
	0 i!

	while (i@ THREADWAITBLOCKS <)
		if (wb@ KeDispatchWaitBlock_Object + @)
			0 wb@ KeDispatchWaitBlockDequeue ok!

			if (DEBUGCHECKS)
				if (ok@)
					"KeThreadWaitBlocksDequeue: couldn't dequeue WaitBlock\n" KeCrash
				end
			end
		end

		1 i +=
		KeDispatchWaitBlock_SIZEOF wb +=
	end

	thread@ KeThread_TimeoutWaitBlock + wb!

	if (wb@ KeDispatchWaitBlock_Object + @)
		0 wb@ KeDispatchWaitBlockDequeue ok!

		if (DEBUGCHECKS)
			if (ok@)
				"KeThreadWaitBlocksDequeue: couldn't dequeue timeout WaitBlock\n" KeCrash
			end
		end
	end
end

fn KeThreadWait { waitipl waitmode alertable -- ok }
	if (DEBUGCHECKS)
		if (KeIPLCurrentGet IPLDPC ~=)
			"KeThreadWait: ipl != IPLDPC\n" KeCrash
		end

		if (waitipl@ IPLDPC >=)
			"KeThreadWait: waitipl >= IPLDPC\n" KeCrash
		end
	end

	auto current
	KeThreadCurrent@ current!

	waitmode@ current@ KeThread_WaitMode + !

	waitipl@ current@ KeThread_WaitIPL + !

	0 current@ KeThread_WaitStatus + !

	if (alertable@)
		THREADSTATUS_WAITINGALERTABLE KeThreadYield
	end else
		THREADSTATUS_WAITINGUNALERTABLE KeThreadYield
	end

	current@ KeThread_WaitStatus + @ ok!
end

fn KeThreadSleep { ms waitmode alertable -- ok }
	0 ok!	

	if (ms@ 0 ==)
		THREADSTATUS_READY KeThreadYield
	end

	auto current
	KeThreadCurrent@ current!

	"KeThreadSleep" 0 current@ KeThread_Timer + KeTimerInitialize ok!

	if (DEBUGCHECKS)
		if (ok@)
			"KeThreadSleep: couldn't initialize timer\n" KeCrash
		end
	end

	0 0 ms@ current@ KeThread_Timer + KeTimerEnqueue ok!

	if (DEBUGCHECKS)
		if (ok@)
			"KeThreadSleep: couldn't enqueue timer\n" KeCrash
		end
	end

	waitmode@ // waitmode
	alertable@ // alertable
	OSWAIT_TIMEOUTINFINITE // timeout
	current@ KeThread_Timer + // object
	KeThreadWaitForObject ok!
end