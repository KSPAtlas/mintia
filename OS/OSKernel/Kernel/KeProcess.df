#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

var KeProcessListHead 0
public KeProcessListHead

fn KeProcessInitialize { name process -- ok }
	process@ KeProcess_Name + name@ KEPROCESSNAMELEN 1 - strncpy

	0 process@ KeProcess_ThreadCount + !
	0 process@ KeProcess_ThreadListHead + !

	0 process@ KeProcess_HeapUsed + !

	0 process@ KeProcess_WorkingSetSize + !

	WORKINGSETDEFAULTMAXIMUM process@ KeProcess_WorkingSetMaximum + !
	WORKINGSETTHRASHMAXIMUM process@ KeProcess_WorkingSetThrashMaximum + !

	0 process@ KeProcess_PageFaultCount + !

	PROCESSSTATUS_READY process@ KeProcess_ProcessStatus + !

	PRIORITY_DEFAULT process@ KeProcess_BasePriority + !

	// put it on lists

	auto rs
	HALCPUInterruptDisable rs!

	// put it on the global process list

	auto h

	KeProcessListHead@ h!

	if (h@)
		process@ h@ KeProcess_GlobalListPrev + !
		h@ process@ KeProcess_GlobalListNext + !
	end

	process@ KeProcessListHead!

	rs@ HALCPUInterruptRestore

	0 ok!
end