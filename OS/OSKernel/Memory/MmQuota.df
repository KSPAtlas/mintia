#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

#include "<inc>/Memory.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OSStatus.h"

fn MmQuotaBlockCreate { vmmax heapmax -- quotablock ok }
	MmQuotaBlock_SIZEOF 'Quot' MmAllocWithTag ok! quotablock!

	if (ok@)
		return
	end

	vmmax@ heapmax@ quotablock@ MmQuotaBlockInitialize ok!
end

fn MmQuotaBlockInitialize { vmmax heapmax quotablock -- ok }
	0 ok!

	1 quotablock@ MmQuotaBlock_References + !

	0 quotablock@ MmQuotaBlock_HeapUsed + !
	heapmax@ quotablock@ MmQuotaBlock_HeapMax + !

	0 quotablock@ MmQuotaBlock_VMUsed + !
	vmmax@ quotablock@ MmQuotaBlock_VMMax + !
end

fn MmQuotaBlockReference { quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	1 quotablock@ MmQuotaBlock_References + +=

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockDereference { quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcount
	quotablock@ MmQuotaBlock_References + @ oldcount!

	if (DEBUGCHECKS)
		if (oldcount@ 0 ==)
			"MmQuotaBlockDereference: references<0\n" KeCrash
		end
	end

	oldcount@ 1 - quotablock@ MmQuotaBlock_References + !

	rs@ HALCPUInterruptRestore

	if (oldcount@ 1 ==)
		// got dereferenced to 0, delete

		quotablock@ MmFree
	end
end

fn MmQuotaBlockCharge { charge quotablock -- ok }
	if (charge@ 0 s<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	0 ok!

	auto heapmax
	quotablock@ MmQuotaBlock_HeapMax + @ heapmax!

	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MmQuotaBlock_HeapUsed + @ oldcharge!

	if (oldcharge@ charge@ + heapmax@ >)
		rs@ HALCPUInterruptRestore

		STATUS_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ + quotablock@ MmQuotaBlock_HeapUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockUncharge { charge quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MmQuotaBlock_HeapUsed + @ oldcharge!

	if (DEBUGCHECKS)
		if (charge@ oldcharge@ >)
			"MmQuotaBlockUncharge: attempt to uncharge more than was charged\n" KeCrash
		end
	end

	oldcharge@ charge@ - quotablock@ MmQuotaBlock_HeapUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockChargeVM { charge quotablock -- ok }
	if (charge@ 0 s<)
		STATUS_INVALID_ARGUMENT ok!

		return
	end

	0 ok!

	auto vmmax
	quotablock@ MmQuotaBlock_VMMax + @ vmmax!

	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MmQuotaBlock_VMUsed + @ oldcharge!

	if (oldcharge@ charge@ + vmmax@ >)
		rs@ HALCPUInterruptRestore

		STATUS_VM_QUOTA_EXCEEDED ok!

		return
	end

	oldcharge@ charge@ + quotablock@ MmQuotaBlock_VMUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockUnchargeVM { charge quotablock -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto oldcharge
	quotablock@ MmQuotaBlock_VMUsed + @ oldcharge!

	if (DEBUGCHECKS)
		if (charge@ oldcharge@ >)
			"MmQuotaBlockUnchargeVM: attempt to uncharge more than was charged\n" KeCrash
		end
	end

	oldcharge@ charge@ - quotablock@ MmQuotaBlock_VMUsed + !

	rs@ HALCPUInterruptRestore
end

fn MmQuotaBlockPrint { quotablock -- }
	quotablock@ MmQuotaBlock_HeapUsed + @ "heapused = %d\n" Printf
	quotablock@ MmQuotaBlock_HeapMax + @ "heapmax = %d\n" Printf
	quotablock@ MmQuotaBlock_VMUsed + @ "vmused = %d\n" Printf
	quotablock@ MmQuotaBlock_VMMax + @ "vmmax = %d\n" Printf
	quotablock@ MmQuotaBlock_References + @ "references = %d\n" Printf
end