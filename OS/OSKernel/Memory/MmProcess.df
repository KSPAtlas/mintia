#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/Object.h"

#include "<inc>/IO.h"

#include "<inc>/Process.h"

#include "<ll>/OSDLL/OS.h"

fn MmProcessInitialize { sectionobject process -- ok }
	// initialize virtual memory related fields

	0 process@ PsProcess_PageFaultCount + !

	0 process@ PsProcess_VADListHead + !
	0 process@ PsProcess_VADListTail + !
	0 process@ PsProcess_VADLookupHint + !
	0 process@ PsProcess_VADMapHint + !

	0 process@ PsProcess_MappedPageFrames + !
	0 process@ PsProcess_CommittedPageFrames + !

	"PsProcessVADListMutex" // name
	KERNELMODE // mode
	process@ PsProcess_VADListMutex + // mutex
	KeMutexInitialize ok!

	if (DEBUGCHECKS)
		if (ok@)
			"MmProcessInitialize: couldn't initialize PsProcessVADListMutex\n" KeCrash
		end
	end

	auto info
	PsOSDLLInformation info!

	// create mappings for OSDLL and PEB section

	// map text section
	info@ PsOSDLLInfo_TextSize + @ // length
	info@ PsOSDLLInfo_TextVAddr + @ // startva
	info@ PsOSDLLInfo_TextOffset + @ // sectionoffset
	PsOSDLLSectionObject@ // sectionobject
	process@ // processobject
	PAGEACCESS_READ PAGEACCESS_EXEC | // pageprotection
	OSMAPFLAG_FIXEDADDRESS // flags
	MmSectionMapViewProcessObject ok! drop

	if (ok@)
		return
	end

	// map data section
	info@ PsOSDLLInfo_DataSize + @ // length
	info@ PsOSDLLInfo_DataVAddr + @ // startva
	info@ PsOSDLLInfo_DataOffset + @ // sectionoffset
	PsOSDLLSectionObject@ // sectionobject
	process@ // processobject
	PAGEACCESS_READ PAGEACCESS_EXEC | PAGEACCESS_WRITECOPY | // pageprotection
	OSMAPFLAG_FIXEDADDRESS // flags
	MmSectionMapViewProcessObject ok! drop

	if (ok@)
		info@ PsOSDLLInfo_TextSize + @ // length
		info@ PsOSDLLInfo_TextVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 1 (%i)\n" KeCrash
			end
		end

		return
	end

	// create bss section

	auto bsssection

	PAGEACCESS_READ PAGEACCESS_WRITE | // pageprotection
	info@ PsOSDLLInfo_BSSSize + @ // anonsize
	0 // fileobject
	process@ PsProcess_OwningUser + @ // owninguser
	ACCESS_OWNER_ALL // permissions
	0 // name
	MmSectionObjectCreateObject ok! bsssection!

	if (ok@)
		info@ PsOSDLLInfo_TextSize + @ // length
		info@ PsOSDLLInfo_TextVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 2 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_DataSize + @ // length
		info@ PsOSDLLInfo_DataVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 3 (%i)\n" KeCrash
			end
		end

		return
	end

	// map bss section
	info@ PsOSDLLInfo_BSSSize + @ // length
	info@ PsOSDLLInfo_BSSVAddr + @ // startva
	0 // sectionoffset
	bsssection@ // sectionobject
	process@ // processobject
	PAGEACCESS_READ PAGEACCESS_WRITE | // pageprotection
	OSMAPFLAG_FIXEDADDRESS // flags
	MmSectionMapViewProcessObject ok! drop

	bsssection@ ObObjectDereferenceByPointer drop

	if (ok@)
		info@ PsOSDLLInfo_TextSize + @ // length
		info@ PsOSDLLInfo_TextVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 4 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_DataSize + @ // length
		info@ PsOSDLLInfo_DataVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 5 (%i)\n" KeCrash
			end
		end

		return
	end

	// map head
	info@ PsOSDLLInfo_HeadSize + @ // length
	OSDLLHEAD // startva
	0 // sectionoffset
	PsOSDLLSectionObject@ // sectionobject
	process@ // processobject
	PAGEACCESS_READ PAGEACCESS_WRITECOPY | // pageprotection
	OSMAPFLAG_FIXEDADDRESS // flags
	MmSectionMapViewProcessObject ok! drop

	if (ok@)
		info@ PsOSDLLInfo_TextSize + @ // length
		info@ PsOSDLLInfo_TextVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 6 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_DataSize + @ // length
		info@ PsOSDLLInfo_DataVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 7 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_BSSSize + @ // length
		info@ PsOSDLLInfo_BSSVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 8 (%i)\n" KeCrash
			end
		end

		return
	end

	if (sectionobject@)
		// map PEB
		OSPEBSIZE // length
		OSPEB // startva
		0 // sectionoffset
		sectionobject@ // sectionobject
		process@ // processobject
		PAGEACCESS_READ PAGEACCESS_WRITE | // pageprotection
		OSMAPFLAG_FIXEDADDRESS // flags
		MmSectionMapViewProcessObject ok! drop

		if (ok@)
			info@ PsOSDLLInfo_TextSize + @ // length
			info@ PsOSDLLInfo_TextVAddr + @ // vaddr
			process@ // processobject
			MmUnmapViewProcess info!

			if (DEBUGCHECKS)
				if (info@)
					info@ "MmProcessInitialize: failed to unmap 9 (%i)\n" KeCrash
				end
			end

			info@ PsOSDLLInfo_DataSize + @ // length
			info@ PsOSDLLInfo_DataVAddr + @ // vaddr
			process@ // processobject
			MmUnmapViewProcess info!

			if (DEBUGCHECKS)
				if (info@)
					info@ "MmProcessInitialize: failed to unmap 10 (%i)\n" KeCrash
				end
			end

			info@ PsOSDLLInfo_BSSSize + @ // length
			info@ PsOSDLLInfo_BSSVAddr + @ // vaddr
			process@ // processobject
			MmUnmapViewProcess info!

			if (DEBUGCHECKS)
				if (info@)
					info@ "MmProcessInitialize: failed to unmap 11 (%i)\n" KeCrash
				end
			end

			info@ PsOSDLLInfo_HeadSize + @ // length
			OSDLLHEAD // vaddr
			process@ // processobject
			MmUnmapViewProcess info!

			if (DEBUGCHECKS)
				if (info@)
					info@ "MmProcessInitialize: failed to unmap 12 (%i)\n" KeCrash
				end
			end

			return
		end
	end

	// create process page directory
	auto pdir
	MmPageDirectoryAlloc ok! pdir!

	if (ok@)
		info@ PsOSDLLInfo_TextSize + @ // length
		info@ PsOSDLLInfo_TextVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 13 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_DataSize + @ // length
		info@ PsOSDLLInfo_DataVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 14 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_BSSSize + @ // length
		info@ PsOSDLLInfo_BSSVAddr + @ // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 15 (%i)\n" KeCrash
			end
		end

		info@ PsOSDLLInfo_HeadSize + @ // length
		OSDLLHEAD // vaddr
		process@ // processobject
		MmUnmapViewProcess info!

		if (DEBUGCHECKS)
			if (info@)
				info@ "MmProcessInitialize: failed to unmap 16 (%i)\n" KeCrash
			end
		end

		if (sectionobject@)
			OSPEBSIZE // length
			OSPEB // vaddr
			process@ // processobject
			MmUnmapViewProcess info!

			if (DEBUGCHECKS)
				if (info@)
					info@ "MmProcessInitialize: failed to unmap 17 (%i)\n" KeCrash
				end
			end
		end

		return
	end

	pdir@ process@ PsProcess_PCB + KeProcess_PageDirectory + !
end

fn MmProcessUninitialize { process -- ok }
	// destroy all VADs, free page tables, etc

	"MmProcessUninitialize: TODO\n" KeCrash
end

fn MmThreadInitialize { thread -- ok }
	// create usermode stack

	auto process
	thread@ KeThread_Process + @ process!

	auto ustack

	PAGEACCESS_READ PAGEACCESS_WRITE | // pageprotection
	USTACKSZ // anonsize
	0 // fileobject
	process@ PsProcess_OwningUser + @ // owninguser
	ACCESS_OWNER_ALL // permissions
	0 // name
	MmSectionObjectCreateObject ok! ustack!

	if (ok@)
		return
	end

	auto ustackbottom

	// map usermode stack
	USTACKSZ // length
	USTACKS // startva
	0 // sectionoffset
	ustack@ // sectionobject
	process@ // processobject
	PAGEACCESS_READ PAGEACCESS_WRITE | // pageprotection
	0 // flags
	MmSectionMapViewProcessObject ok! ustackbottom!

	ustack@ ObObjectDereferenceByPointer drop

	if (ok@)
		return
	end

	ustackbottom@ thread@ PsThread_UserStackBottom + !
	ustackbottom@ USTACKSZ + thread@ PsThread_UserStackTop + !
end

fn MmThreadUninitialize { thread -- ok }
	auto process
	thread@ KeThread_Process + @ process!

	// unmap usermode stack
	USTACKSZ // length
	thread@ PsThread_UserStackBottom + @ // vaddr
	process@ // processobject
	MmUnmapViewProcess ok!
end