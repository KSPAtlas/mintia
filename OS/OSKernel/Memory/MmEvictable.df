#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"

#include "<inc>/HALCPU.h"

#include "<inc>/HALMap.h"

#include "<inc>/Kernel.h"

#include "<inc>/Memory.h"

#include "<ll>/OSDLL/OSStatus.h"

var MmEvictablePageListHead 0
public MmEvictablePageListHead

var MmEvictablePageListTail 0
public MmEvictablePageListTail

var MmEvictableFastPageListHead 0
public MmEvictableFastPageListHead

var MmEvictableFastPageListTail 0
public MmEvictableFastPageListTail

fn MmEvictablePageAlloc { flags evictablefuncs priority -- pfdbe pfn ok }
	priority@ // priority
	MmPageAlloc ok! pfn! pfdbe!

	if (ok@)
		return
	end

	1 pfdbe@ MmPageFrameEntryEvictable_References + !
	
	0 pfdbe@ MmPageFrameEntryEvictable_Next + !
	0 pfdbe@ MmPageFrameEntryEvictable_Prev + !

	flags@ pfdbe@ MmPageFrameEntryEvictable_EvictionFlags + !

	evictablefuncs@ pfdbe@ MmPageFrameEntryEvictable_EvictableFunctions + !
end

fn MmPageEvict { pfdbe -- evicted ok }
	auto evictionfuncs
	pfdbe@ MmPageFrameEntryEvictable_EvictableFunctions + @ evictionfuncs!

	if (DEBUGCHECKS)
		if (evictionfuncs@ ~~)
			"MmPageEvict: no eviction funcs for page\n" KeCrash
		end
	end

	auto evictionfunc
	evictionfuncs@ MmEvictableFunctions_EvictionFunc + @ evictionfunc!

	if (evictionfunc@)
		pfdbe@ // pfdbe
		evictionfunc@ MmEvictionFunction ok! evicted!
	end else
		0 ok!
		1 evicted!
	end

	if (evicted@)
		// evicted, free the page

		if (DEBUGCHECKS)
			if (pfdbe@ MmPageFrameEntryEvictable_References + @ 1 ~=)
				pfdbe@ MmPageFrameEntryEvictable_References + @
				"MmPageEvict: attempt to evict a page with %d references\n" KeCrash
			end
		end

		pfdbe@ MmPageFrameEntryEvictable_PFN + @ // pfn
		MmPageFree
	end
end

fn MmEvictablePageDereference { pfdbe -- oldcount }
	auto evictionfuncs
	pfdbe@ MmPageFrameEntryEvictable_EvictableFunctions + @ evictionfuncs!

	if (DEBUGCHECKS)
		if (evictionfuncs@ ~~)
			"MmEvictablePageDereference: no eviction funcs for page\n" KeCrash
		end
	end

	auto dereffunc
	evictionfuncs@ MmEvictableFunctions_DereferenceFunc + @ dereffunc!

	if (dereffunc@)
		pfdbe@ MmPageFrameEntryEvictable_References + @ // oldcount (not guaranteed to be accurate...)
		pfdbe@ // pfdbe
		dereffunc@ MmDereferenceFunction
	end

	auto rs
	HALCPUInterruptDisable rs!

	pfdbe@ MmPageFrameEntryEvictable_References + @ oldcount!

	if (DEBUGCHECKS)
		if (oldcount@ 0 ==)
			"MmEvictablePageDereference: underflow\n" KeCrash
		end
	end

	oldcount@ 1 - pfdbe@ MmPageFrameEntryEvictable_References + !

	if (oldcount@ 1 ==)
		// no refs now, add to back of evictable page LRU list

		auto t

		if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlags + @ MMEVICTFLAG_FAST &)
			MmEvictableFastPageListTail@ t!

			if (t@ ~~)
				0 pfdbe@ MmPageFrameEntryEvictable_Next + !
				0 pfdbe@ MmPageFrameEntryEvictable_Prev + !

				pfdbe@ MmEvictableFastPageListHead!
				pfdbe@ MmEvictableFastPageListTail!
			end else
				0 pfdbe@ MmPageFrameEntryEvictable_Next + !

				t@ pfdbe@ MmPageFrameEntryEvictable_Prev + !
				pfdbe@ t@ MmPageFrameEntryEvictable_Next + !
				pfdbe@ MmEvictableFastPageListTail!
			end
		end else
			MmEvictablePageListTail@ t!

			if (t@ ~~)
				0 pfdbe@ MmPageFrameEntryEvictable_Next + !
				0 pfdbe@ MmPageFrameEntryEvictable_Prev + !

				pfdbe@ MmEvictablePageListHead!
				pfdbe@ MmEvictablePageListTail!
			end else
				0 pfdbe@ MmPageFrameEntryEvictable_Next + !

				t@ pfdbe@ MmPageFrameEntryEvictable_Prev + !
				pfdbe@ t@ MmPageFrameEntryEvictable_Next + !
				pfdbe@ MmEvictablePageListTail!
			end
		end
	end

	rs@ HALCPUInterruptRestore
end

fn MmEvictablePageReference { pfdbe -- oldcount }
	auto rs
	HALCPUInterruptDisable rs!

	pfdbe@ MmPageFrameEntryEvictable_References + @ oldcount!

	oldcount@ 1 + pfdbe@ MmPageFrameEntryEvictable_References + !

	if (oldcount@ 0 ==)
		// ref'd, remove from evictable page LRU list

		auto ls
		pfdbe@ MmPageFrameEntryEvictable_Prev + @ ls!

		auto ns
		pfdbe@ MmPageFrameEntryEvictable_Next + @ ns!

		if (ls@)
			ns@ ls@ MmPageFrameEntryEvictable_Next + !
		end else
			if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlags + @ MMEVICTFLAG_FAST &)
				ns@ MmEvictableFastPageListHead!
			end else
				ns@ MmEvictablePageListHead!
			end
		end

		if (ns@)
			ls@ ns@ MmPageFrameEntryEvictable_Prev + !
		end else
			if (pfdbe@ MmPageFrameEntryEvictable_EvictionFlags + @ MMEVICTFLAG_FAST &)
				ls@ MmEvictableFastPageListTail!
			end else
				ls@ MmEvictablePageListTail!
			end
		end

		0 pfdbe@ MmPageFrameEntryEvictable_Prev + !
		0 pfdbe@ MmPageFrameEntryEvictable_Next + !
	end

	rs@ HALCPUInterruptRestore

	auto evictionfuncs
	pfdbe@ MmPageFrameEntryEvictable_EvictableFunctions + @ evictionfuncs!

	if (DEBUGCHECKS)
		if (evictionfuncs@ ~~)
			"MmEvictablePageDereference: no eviction funcs for page\n" KeCrash
		end
	end

	auto reffunc
	evictionfuncs@ MmEvictableFunctions_ReferenceFunc + @ reffunc!

	if (reffunc@)
		oldcount@ // oldcount
		pfdbe@ // pfdbe
		reffunc@ MmReferenceFunction
	end
end