#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDebug.h"

#include "<inc>/Kernel.h"

#include "<inc>/Memory.h"

#include "<ll>/OSDLL/OSStatus.h"

fn MmKernelStackAlloc { -- kstack ok }
	FREEFIRST CANBLOCK | // priority
	MmPageAlloc ok! kstack! drop

	if (ok@)
		return
	end

	PAGESHIFT kstack <<=
	IDENTITYSPACE kstack |=
end

fn MmKernelStackFree { kstack -- }
	IDENTITYSPACE ~ kstack &=
	kstack@ PAGESHIFT >> MmPageFree
end

fn MmPageDirectoryAlloc { -- pdir ok }
	ZEROMUST CANBLOCK | // priority
	MmPageAlloc ok! pdir! drop

	if (ok@)
		return
	end

	PAGESHIFT pdir <<=
	IDENTITYSPACE pdir |=

	auto kdir
	HALPlatformKernelPageDirectory@ IDENTITYSPACE | kdir!

	// identically map kernel space into the new page directory...

	pdir@ 2048 + // dest
	kdir@ 2048 + // src
	2048 // size
	memcpy
end

fn MmPageDirectoryFree { pdir -- }
	IDENTITYSPACE ~ pdir &=
	pdir@ PAGESHIFT >> MmPageFree
end