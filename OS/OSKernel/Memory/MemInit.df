#include "<df>/dragonfruit.h"
#include "<inc>/HALLog.h"

#include "<inc>/KeCrash.h"

#include "<inc>/Mem.h"

externptr HALLoaderPFDB

externptr HALLoaderInitialBitmap

externptr HALLoaderInitialBitmapSize

externptr HALLoaderTotalRAM

externptr HALLoaderHeap

externptr HALLoaderHeapSize

var MemPageFrameDB 0
public MemPageFrameDB

var MemPageFreeListHead 0
public MemPageFreeListHead

table MemBuckets[BUCKETS]
public MemBuckets

var MemPageTotal 0
public MemPageTotal

var MemPageFreeCount 0
public MemPageFreeCount

var MemHeapSize 0
public MemHeapSize

var MemHeapBytesFree 0
public MemHeapBytesFree

var MemHeap 0
public MemHeap

fn MemInit { -- }
	auto ptr

	HALLoaderPFDB@ dup MemPageFrameDB! ptr!

	auto i
	0 i!

	auto pages
	HALLoaderTotalRAM@ 12 >> pages!

	auto bmp
	HALLoaderInitialBitmap@ bmp!

	auto sz
	HALLoaderInitialBitmapSize@ 32 * sz!

	auto word

	auto last
	0 last!

	// initialize the page DB by linking all of the entries into the
	// free list, except those marked allocated in the loader bitmap.

	while (i@ pages@ <)
		if (i@ sz@ <)
			if (i@ 31 & ~~)
				bmp@@ word!
				4 bmp +=
			end

			if (word@ i@ 31 & bitget ~~)
				1 MemPageFreeCount +=

				if (last@)
					ptr@ last@ PFDBEFree_Next + !
				end else
					ptr@ MemPageFreeListHead!
				end

				ptr@ last!
			end
		end else
			1 MemPageFreeCount +=

			if (last@)
				ptr@ last@ PFDBEFree_Next + !
			end else
				ptr@ MemPageFreeListHead!
			end

			ptr@ last!
		end

		i@ ptr@ PFDBE_PFN + !

		PFDBE_SIZEOF ptr +=
		1 i +=
	end

	MemPageFreeCount@ MemPageTotal!

	// initialize the heap as one block on the free list

	HALLoaderHeap@ ptr!

	MEMBLOCKMAGIC ptr@ MemBlock_Magic + !
	0 ptr@ MemBlock_Next + !
	0 ptr@ MemBlock_Prev + !
	MEMBLOCKFREE ptr@ MemBlock_Tag + !

	0 i!
	1 BUCKETSPO2START << sz!
	0 word!

	while (i@ BUCKETS <)
		if (HALLoaderHeapSize@ sz@ ==)
			i@ ptr@ MemBlock_BucketIndex + !

			// found the correct bucket
			ptr@ [i@]MemBuckets!

			1 word!

			break
		end

		1 sz <<=
		1 i +=
	end

	HALLoaderHeapSize@ MemHeapSize!
	HALLoaderHeapSize@ MemHeapBytesFree!
	HALLoaderHeap@ MemHeap!

	if (word@ ~~)
		"MemInit: couldn't initialize heap bucket\n" KeCrash
	end

	"Pre-boot memory statistics:\n" "MemInit" HALLog

	MemUsageDump
end

fn MemUsageDump { -- }
	"AVAIL" "TOTAL" "MEMORY" "  %12s %12s %12s\n" Printf

	"Page Frames" "  %12s " Printf
	MemPageTotal@ "%12d " Printf
	MemPageFreeCount@ "%12d\n" Printf

	"Heap Bytes" "  %12s " Printf
	MemHeapSize@ "%12d " Printf
	MemHeapBytesFree@ "%12d\n" Printf
end