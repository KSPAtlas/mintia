#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/Security.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

fn IODeviceCreate { name driver permissions -- deviceobject ok }
	if (driver@ IODriver_VersionMajor + @ IOVERSION_MAJOR ~=)
		name@ "Failed to create device '%s'.\n" "IODeviceCreate" HALLog

		-1 ok!

		return
	end

	if (driver@ IODriver_VersionMinor + @ IOVERSION_MINOR ~=)
		name@ "Device '%s' has mismatched driver interface.\n" "IODeviceCreate" HALLog
	end

	auto dir

	name@ // name
	OBFLAGS_PERMANENT // flags
	permissions@ // permissions
	SeSystemUser@ // owninguser
	IODevice_SIZEOF driver@ IODriver_ExtensionSize + @ + // bodysize
	IODeviceTypeObject@ // type
	ObObjectCreate ok! deviceobject!

	if (ok@)
		ok@ name@ "IODeviceCreate: failed to create object for device '%s' (%i)\n" KeCrash
	end

	auto fcb

	driver@ IODriver_DispatchTable + @ // dispatchtable
	deviceobject@ // devobj
	driver@ IODriver_DeviceType + @ // filetype
	0 // flags
	IOFileControlBlockCreate ok! fcb!

	if (ok@)
		ok@ name@ "IODeviceCreate: failed to create FCB for device '%s' (%i)\n" KeCrash
	end

	driver@                         deviceobject@ IODevice_Driver + !
	deviceobject@ IODevice_SIZEOF + deviceobject@ IODevice_Extension + !
	fcb@                            deviceobject@ IODevice_FileControlBlock + !

	deviceobject@ ObHeader_SIZEOF - // object
	IODevicesDirectoryObject@ // directory
	ObDirectoryInsertByPointers ok!

	if (ok@)
		ok@ name@ "IODeviceCreate: failed to insert device '%s' into /Devices (%i)\n" KeCrash
	end
end