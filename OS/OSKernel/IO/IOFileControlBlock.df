#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALDriver.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Object.h"

#include "<inc>/Process.h"

#include "<inc>/Security.h"

#include "<inc>/IO.h"

#include "<ll>/OSDLL/OS.h"

fn IOFileControlBlockInitialize { dispatchtable devobj filetype flags fcb -- ok }
	devobj@ fcb@ IOFileControlBlock_DeviceObject + !
	filetype@ fcb@ IOFileControlBlock_FileType + !

	flags@ fcb@ IOFileControlBlock_Flags + !
	0 fcb@ IOFileControlBlock_CacheInfoBlock + !
	0 fcb@ IOFileControlBlock_References + !
	0 fcb@ IOFileControlBlock_SizeInBytes + !
	dispatchtable@ fcb@ IOFileControlBlock_DispatchTable + !
	0 fcb@ IOFileControlBlock_FSContext + !
	0 fcb@ IOFileControlBlock_Mount + !
	0 fcb@ IOFileControlBlock_Busy + !
	0 fcb@ IOFileControlBlock_ParseCount + !

	"FCBRwLock" // name
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockInitialize ok!

	if (ok@)
		"IOFileControlBlockCreate: rwlock initialization failed\n" KeCrash
	end

	fcb@ IOFileControlBlock_AccessTime + // ptr
	KeTime_SIZEOF // size
	0 // word
	memset

	fcb@ IOFileControlBlock_ModifyTime + // ptr
	KeTime_SIZEOF // size
	0 // word
	memset

	fcb@ IOFileControlBlock_ChangeTime + // ptr
	KeTime_SIZEOF // size
	0 // word
	memset
end

fn IOFileControlBlockCreate { dispatchtable devobj filetype flags -- fcb ok }
	IOFileControlBlock_SIZEOF // bytes
	'IOFC' // tag
	MmAllocWithTag ok! fcb!

	if (ok@)
		return
	end

	dispatchtable@ // dispatchtable
	devobj@ // devobj
	filetype@ // filetype
	flags@ // flags
	fcb@ // fcb
	IOFileControlBlockInitialize ok!
end

fn IOFileControlBlockDelete { writeout fcb -- }
	auto cacheblock
	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

	if (cacheblock@)
		if (writeout@ -1 ==)
			"IOFileControlBlockDelete: didn't expect a cacheblock\n" KeCrash
		end

		auto ok

		writeout@ // writeout
		cacheblock@ // cacheblock
		IOCacheInfoBlockDestroy ok!

		if (DEBUGCHECKS)
			if (ok@)
				"IOFileControlBlockDelete: failed to destroy cacheblock\n" KeCrash
			end
		end
	end

	fcb@ MmFree
end

fn IOFileControlBlockLock { fcb -- ok }
	1 // alertable
	0 // nowait
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockAcquireExclusive ok!
end

fn IOFileControlBlockUnlock { fcb -- }
	fcb@ IOFileControlBlock_RwLock + ExRwLockRelease
end

fn IOFileControlBlockLockShared { fcb -- ok }
	1 // alertable
	0 // nowait
	0 // canstarve
	fcb@ IOFileControlBlock_RwLock + // rwlock
	ExRwLockAcquireShared ok!
end

fn IOFileControlBlockReference { fcb -- oldcount }
	// should be called by filesystem driver in its parse method, if needed.

	auto rs
	HALCPUInterruptDisable rs!

	fcb@ IOFileControlBlock_References + @ oldcount!

	oldcount@ 1 + fcb@ IOFileControlBlock_References + !

	rs@ HALCPUInterruptRestore
end

fn IOFileControlBlockDereference { fcb -- oldcount }
	// should be called by filesystem driver in its delete method, if needed.

	auto rs
	HALCPUInterruptDisable rs!

	fcb@ IOFileControlBlock_References + @ oldcount!

	if (DEBUGCHECKS)
		if (oldcount@ 0 ==)
			"IOFileControlBlockDereference: refcount underflow\n" KeCrash
		end
	end

	oldcount@ 1 - fcb@ IOFileControlBlock_References + !

	rs@ HALCPUInterruptRestore
end

fn IOFileControlBlockCacheCheck { wantcaching fcb -- cacheblock ok }
	fcb@ IOFileControlBlockLock ok!

	if (ok@)
		return
	end

	fcb@ IOFileControlBlock_CacheInfoBlock + @ cacheblock!

	if (wantcaching@)
		if (fcb@ IOFileControlBlock_UncachedIOCount + @)
			STATUS_IO_TRY_AGAIN_LATER ok!

			fcb@ IOFileControlBlockUnlock

			return
		end

		if (cacheblock@ ~~)
			// still no cache block, plus we want there to be one.
			// therefore, initiate caching

			fcb@ // fcb
			IOCacheInitialize ok! cacheblock!

			if (ok@)
				// failed to initialize caching, do this I/O operation
				// uncached and try again later...

				0 cacheblock!
			end
		end
	end elseif (cacheblock@)
		if (fcb@ IOFileControlBlock_References + @ 1 ==)
			1 // writeout
			cacheblock@ // cacheblock
			IOCacheInfoBlockDestroy ok!

			if (DEBUGCHECKS)
				if (ok@)
					ok@ "IOFileControlBlockAcquireCheck: failed to destroy cacheblock (%i)\n" KeCrash
				end
			end

			0 fcb@ IOFileControlBlock_CacheInfoBlock + !

			0 cacheblock!
		end
	end

	if (cacheblock@ ~~)
		1 fcb@ IOFileControlBlock_UncachedIOCount + +=
	end

	fcb@ IOFileControlBlockUnlock
end