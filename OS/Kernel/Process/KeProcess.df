#include "<df>/dragonfruit.h"
#include "<inc>/HALLog.h"

#include "<inc>/KeCrash.h"

#include "<inc>/KeMemAlloc.h"
#include "<inc>/KeProcess.h"

#include "<inc>/KeSecurity.h"

#include "<inc>/KeIPL.h"

var KeProcessListHead 0
public KeProcessListHead

fn KeProcessInitialize { user name process -- ok }
	process@ KeProcess_Name + name@ KEPROCESSNAMELEN 1 - strncpy

	0 process@ KeProcess_ThreadCount + !
	0 process@ KeProcess_ThreadListHead + !

	0 process@ KeProcess_HeapUsed + !

	0 process@ KeProcess_WorkingSetSize + !

	WORKINGSETDEFAULTMAXIMUM process@ KeProcess_WorkingSetMaximum + !
	WORKINGSETTHRASHMAXIMUM process@ KeProcess_WorkingSetThrashMaximum + !

	0 process@ KeProcess_PageFaultCount + !

	user@ process@ KeProcess_User + !

	PROCESSSTATUS_READY process@ KeProcess_ProcessStatus + !

	PRIORITY_DEFAULT process@ KeProcess_BasePriority + !

	// put it on lists

	auto ipl
	IPLDPC KeIPLRaise ipl!

	// put it on the global process list

	auto h

	KeProcessListHead@ h!

	if (h@)
		process@ h@ KeProcess_GlobalListPrev + !
		h@ process@ KeProcess_GlobalListNext + !
	end

	process@ KeProcessListHead!

	// put it on the user's process list

	user@ KeSecurityUser_ProcessListHead + @ h!

	if (h@)
		process@ h@ KeProcess_UserListPrev + !
		h@ process@ KeProcess_UserListNext + !
	end

	process@ user@ KeSecurityUser_ProcessListHead + !

	ipl@ KeIPLLower

	0 ok!
end