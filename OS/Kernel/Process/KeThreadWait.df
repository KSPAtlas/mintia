#include "<df>/dragonfruit.h"
#include "<inc>/HALLog.h"

#include "<inc>/HALCPU.h"
#include "<inc>/HALRTC.h"

#include "<inc>/KeCrash.h"

#include "<inc>/KeMemAlloc.h"

#include "<inc>/KeDPC.h"
#include "<inc>/KeTime.h"
#include "<inc>/KeDispatch.h"
#include "<inc>/KeTimer.h"

#include "<inc>/KeMutex.h"

#include "<inc>/KeProcess.h"

#include "<inc>/KeIPL.h"

#include "<ll>/OSDLL/OSStatus.h"

// initializes a dispatch object's header

fn KeDispatchInitialize { name type dobject -- ok }
	0 ok!

	0 dobject@ KeDispatchHeader_Signaled + !

	type@ dobject@ KeDispatchHeader_Type + !

	name@ dobject@ KeDispatchHeader_Name + !

	0 dobject@ KeDispatchHeader_WaitBlockListHead + !
	0 dobject@ KeDispatchHeader_WaitBlockListTail + !
end

// initializes a WaitBlock

fn KeDispatchWaitBlockInitialize { thread waitblock -- ok }
	0 ok!

	0 waitblock@ KeDispatchWaitBlock_Prev + !
	0 waitblock@ KeDispatchWaitBlock_Next + !

	0 waitblock@ KeDispatchWaitBlock_WaitStatusReturn + !
	0 waitblock@ KeDispatchWaitBlock_WaitType + !

	thread@ waitblock@ KeDispatchWaitBlock_Thread + !

	0 waitblock@ KeDispatchWaitBlock_Object + !
end

fn KeDispatchWaitBlockDequeue { waitstatus waitblock -- ok }
	0 ok!

	if (KeIPLCurrentGet IPLDPC ~=)
		"KeDispatchWaitBlockDequeue: ipl != IPLDPC\n" KeCrash
	end

	if (waitblock@ KeDispatchWaitBlock_Object + @ ~~)
		"KeDispatchWaitBlockDequeue: attempt to dequeue a non-enqueued WaitBlock\n" KeCrash
	end

	auto object
	waitblock@ KeDispatchWaitBlock_Object + @ object!

	auto n
	waitblock@ KeDispatchWaitBlock_Next + @ n!

	if (n@)
		waitblock@ KeDispatchWaitBlock_Prev + @ n@ KeDispatchWaitBlock_Prev + !
	end else // no next means we were the tail
		waitblock@ KeDispatchWaitBlock_Prev + @ object@ KeDispatchHeader_WaitBlockListTail + !
	end

	auto p
	waitblock@ KeDispatchWaitBlock_Prev + @ p!

	if (p@)
		waitblock@ KeDispatchWaitBlock_Next + @ p@ KeDispatchWaitBlock_Next + !
	end else // no prev means we were the head
		waitblock@ KeDispatchWaitBlock_Next + @ object@ KeDispatchHeader_WaitBlockListHead + !
	end

	0 waitblock@ KeDispatchWaitBlock_Next + !
	0 waitblock@ KeDispatchWaitBlock_Prev + !

	0 waitblock@ KeDispatchWaitBlock_Object + !

	waitstatus@ waitblock@ KeDispatchWaitBlock_WaitStatusReturn + !
end

// adds a WaitBlock to the tail of a dispatch object's WaitBlockList

fn KeDispatchWaitBlockEnqueue { waittype waitblock object -- ok }
	0 ok!

	if (KeIPLCurrentGet IPLDPC ~=)
		"KeDispatchWaitBlockInsert: ipl != IPLDPC\n" KeCrash
	end

	if (waitblock@ KeDispatchWaitBlock_Object + @)
		"KeDispatchWaitBlockEnqueue: attempt to queue an enqueued WaitBlock\n" KeCrash
	end

	auto t
	object@ KeDispatchHeader_WaitBlockListTail + @ t!

	if (t@ ~~)
		waitblock@ object@ KeDispatchHeader_WaitBlockListHead + !
	end else
		waitblock@ t@ KeDispatchWaitBlock_Next + !
		t@ waitblock@ KeDispatchWaitBlock_Prev + !
	end

	waitblock@ object@ KeDispatchHeader_WaitBlockListTail + !

	object@ waitblock@ KeDispatchWaitBlock_Object + !

	waittype@ waitblock@ KeDispatchWaitBlock_WaitType + !
end

// satisfies a WaitBlock

fn KeDispatchWaitBlockSatisfy { waitstatus priboost waitblock -- ok }
	if (KeIPLCurrentGet IPLDPC ~=)
		"KeDispatchWaitBlockSatisfy: ipl != IPLDPC\n" KeCrash
	end

	waitstatus@ waitblock@ KeDispatchWaitBlockDequeue ok!

	if (ok@)
		return
	end

	auto thread
	waitblock@ KeDispatchWaitBlock_Thread + @ thread!

	1 thread@ KeThread_WaitCount + -=

	auto wt
	waitblock@ KeDispatchWaitBlock_WaitType + @ wt!

	if (wt@ THREADWAITANY ==)
		waitstatus@ priboost@ thread@ KeThreadWakeup drop
	end elseif (wt@ THREADWAITALL ==)
		if (thread@ KeThread_WaitCount + @ 0 ==)
			0 priboost@ thread@ KeThreadWakeup drop
		end
	end
end

// satisfies all waiters on a dispatch object
// this SIGNALS the object

fn KeDispatchSatisfyAll { waitstatus priboost object -- count ok }
	0 ok!

	0 count!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	1 object@ KeDispatchHeader_Signaled + !

	auto h
	object@ KeDispatchHeader_WaitBlockListHead + @ h!

	auto wb

	while (h@)
		h@ wb!
		h@ KeDispatchWaitBlock_Next + @ h!

		if (waitstatus@ priboost@ wb@ KeDispatchWaitBlockSatisfy)
			"KeDispatchSatisfyAll: couldn't satisfy WaitBlock\n" KeCrash
		end

		1 count +=
	end

	ipl@ KeIPLLower
end

// satisfies the highest priority, earliest wait on a dispatch object
// this DOES NOT SIGNAL the object

fn KeDispatchSatisfyFirst { waitstatus priboost object -- wokenthread ok }
	0 ok!

	0 wokenthread!

	auto wthpri
	0 wthpri!

	auto wokewb
	0 wokewb!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto h
	object@ KeDispatchHeader_WaitBlockListHead + @ h!

	while (h@)
		auto nw
		h@ KeDispatchWaitBlock_Thread + @ nw!

		auto npri

		if (wokenthread@ ~~)
			h@ wokewb!

			nw@ wokenthread!

			nw@ KeThread_BasePriority + @ wthpri!
		end else
			nw@ KeThread_BasePriority + @ npri!

			if (npri@ wthpri@ >)
				h@ wokewb!

				nw@ wokenthread!

				npri@ wthpri!
			end
		end

		h@ KeDispatchWaitBlock_Next + @ h!
	end

	if (wokewb@)
		if (waitstatus@ priboost@ wokewb@ KeDispatchWaitBlockSatisfy)
			"KeDispatchSatisfyFirst: couldn't satisfy WaitBlock\n" KeCrash
		end
	end

	ipl@ KeIPLLower
end

fn KeThreadWaitForMultipleObjects { waitmode alertable waittype objectcount objecttable -- ok }
	0 ok!

	if (objectcount@ 0 ==)
		return
	end

	if (objectcount@ THREADWAITBLOCKS >)
		"KeThreadWaitForMultipleObjects: objectcount>THREADWAITBLOCKS\n" KeCrash
	end

	auto current
	KeThreadCurrent@ current!

	auto waitipl
	IPLDPC KeIPLRaise waitipl!

	// do below but like with multiple things

	while (1)
		auto i
		0 i!

		auto waitblock
		current@ KeThread_WaitBlocks + waitblock! // now points to current thread's first waitblock

		auto satisfied

		if (waittype@ THREADWAITANY ==)
			0 satisfied!
		end else
			1 satisfied!
		end

		objectcount@ current@ KeThread_WaitCount + !

		while (i@ objectcount@ <)
			auto object
			objecttable@ i@ 4 * + @ object!

			if (waittype@ THREADWAITANY ==)
				if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_MUTEX ==)
					if (object@ KeMutex_OwningThread + @ current@ ==)
						1 object@ KeMutex_Locked + +=

						1 satisfied!

						break
					end elseif (object@ KeDispatchHeader_Signaled + @)
						if (current@ object@ KeMutexOwnerSet)
							"KeThreadWaitForMultipleObjects: couldn't KeMutexOwnerSet\n" KeCrash
						end

						1 satisfied!

						break
					end
				end else
					if (object@ KeDispatchHeader_Signaled + @)
						1 satisfied!

						break
					end
				end
			end else
				if (object@ KeDispatchHeader_Signaled + @ ~~)
					if (object@ KeDispatchHeader_Type + @ DISPATCHOBJECT_MUTEX ==)
						if (object@ KeMutex_OwningThread + @ current@ ~=)
							0 satisfied!
						end
					end else
						0 satisfied!
					end
				end
			end

			if (waittype@ waitblock@ object@ KeDispatchWaitBlockEnqueue)
				"KeThreadWaitForMultipleObjects: couldn't KeDispatchWaitBlockEnqueue\n" KeCrash
			end

			KeDispatchWaitBlock_SIZEOF waitblock +=
			1 i +=
		end

		if (satisfied@)
			// the wait is already satisfied. dequeue any wait blocks we just enqueued
			// and then return.

			current@ KeThread_WaitBlocks + waitblock!

			0 i!

			while (i@ objectcount@ <)
				if (waitblock@ KeDispatchWaitBlock_Object + @)
					if (0 waitblock@ KeDispatchWaitBlockDequeue)
						"KeThreadWaitForMultipleObjects: couldn't dequeue WaitBlock\n" KeCrash
					end
				end

				1 i +=
				KeDispatchWaitBlock_SIZEOF waitblock +=
			end

			break
		end

		waitipl@ waitmode@ alertable@ KeThreadWait ok!

		// if we got woken up to deliver an APC, it will be executed here when we
		// lower the IPL.
		// the object may become signaled while this occurs, hence the check above.

		if (waitipl@ IPLLOW ==)
			IPLLOW KeIPLLower

			// we were awoken to deliver a kernel APC. raise back to IPLDPC and retry.

			IPLDPC KeIPLRaise drop
		end

		if (ok@ STATUS_KERNEL_APC ~=)
			break
		end
	end

	waitipl@ KeIPLLower
end

fn KeThreadWaitForObject { waitmode alertable object -- ok }
	auto objecttable
	4 alloc objecttable!

	object@ objecttable@!

	waitmode@ alertable@ THREADWAITANY 1 objecttable@ KeThreadWaitForMultipleObjects ok!
end

fn KeThreadWakeup { waitstatus priboost thread -- woken }
	if (KeIPLCurrentGet IPLDPC ~=)
		"KeThreadWakeup: ipl != IPLDPC\n" KeCrash
	end

	0 woken!

	auto status
	thread@ KeThread_Status + @ status!

	if (status@ THREADSTATUS_TERMINATED ==)
		// XXX are there legitimate, normal situations where you might try to wake up a dead thread
		// that has not yet been cleaned up?

		"KeThreadWakeup: thread is already dead\n" KeCrash
	end

	if (status@ THREADSTATUS_WAITINGALERTABLE ~= status@ THREADSTATUS_WAITINGUNALERTABLE ~= &&)
		// thread isn't waiting

		return
	end

	auto wb
	thread@ KeThread_WaitBlocks + wb!

	auto i
	0 i!

	while (i@ THREADWAITBLOCKS <)
		if (wb@ KeDispatchWaitBlock_Object + @)
			if (0 wb@ KeDispatchWaitBlockDequeue)
				"KeThreadWakeup: couldn't dequeue WaitBlock\n" KeCrash
			end
		end

		1 i +=
		KeDispatchWaitBlock_SIZEOF wb +=
	end

	priboost@ thread@ KeThreadPriorityBoost
	waitstatus@ thread@ KeThread_WaitStatus + !
	QUEUEBACK thread@ KeThreadReady

	1 woken!
end

fn KeThreadWait { waitipl waitmode alertable -- ok }
	if (KeIPLCurrentGet IPLDPC ~=)
		"KeThreadWait: ipl != IPLDPC\n" KeCrash
	end

	if (waitipl@ IPLDPC >=)
		"KeThreadWait: waitipl >= IPLDPC\n" KeCrash
	end

	auto current
	KeThreadCurrent@ current!

	waitmode@ current@ KeThread_WaitMode + !

	waitipl@ current@ KeThread_WaitIPL + !

	0 current@ KeThread_WaitStatus + !

	if (alertable@)
		THREADSTATUS_WAITINGALERTABLE KeThreadYield
	end else
		THREADSTATUS_WAITINGUNALERTABLE KeThreadYield
	end

	current@ KeThread_WaitStatus + @ ok!
end

fn KeThreadSleep { ms waitmode alertable -- ok }
	auto current
	KeThreadCurrent@ current!

	"KeThreadSleep" 0 current@ KeThread_Timer + KeTimerInitialize ok!

	if (ok@)
		"KeThreadSleep: couldn't initialize timer\n" KeCrash
	end

	if (0 0 ms@ current@ KeThread_Timer + KeTimerEnqueue)
		"KeThreadSleep: couldn't enqueue timer\n" KeCrash
	end

	waitmode@ alertable@ current@ KeThread_Timer + KeThreadWaitForObject ok!
end