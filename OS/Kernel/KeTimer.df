#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"

#include "<inc>/KeIPL.h"

#include "<inc>/KeCrash.h"

#include "<inc>/KeDPC.h"
#include "<inc>/KeTime.h"
#include "<inc>/KeDispatch.h"
#include "<inc>/KeTimer.h"

#include "<inc>/KeMemAlloc.h"

#include "<inc>/HALRTC.h"

#include "<ll>/OSDLL/OSStatus.h"

var KeTimerListHead 0
public KeTimerListHead

var KeTimerListTail 0

buffer KeTimerExpirationEarliest KeTime_SIZEOF
public KeTimerExpirationEarliest

buffer KeTimerExpirationDPC KeDPC_SIZEOF
public KeTimerExpirationDPC

fn (DPCFunction) KeTimerExpiration { context1 context2 -- }
	auto timeatexpiry
	KeTime_SIZEOF alloc timeatexpiry!

	context1@ timeatexpiry@ KeTime_SecPart + !
	context2@ timeatexpiry@ KeTime_MsPart + !

	auto timer
	KeTimerListHead@ timer!

	0 KeTimerListHead!

	auto earliest
	KeTimerExpirationEarliest earliest!

	while (timer@)
		auto time
		timer@ KeTimer_ExpiryTime + time!

		if (time@ timeatexpiry@ KeTimeCompare TIMELATER ==)
			time@ KeTime_SecPart + @ earliest@ KeTime_SecPart + !
			time@ KeTime_MsPart + @ earliest@ KeTime_MsPart + !

			return
		end

		auto ok
		STATUS_WAIT_TIMEOUT 0 timer@ KeDispatchSatisfyAll ok! drop

		if (ok@)
			"KeTimerExpiration: couldn't satisfy timer waits\n" KeCrash
		end

		if (timer@ KeTimer_DPC + KeDPC_Function + @)
			timer@ KeTimer_Context1 + @ timer@ KeTimer_Context2 + @ DPCLOWIMPORTANCE timer@ KeTimer_DPC + KeDPCEnqueue
		end

		auto ntimer
		timer@ KeTimer_Next + @ ntimer!

		timer@ KeTimerDequeue drop

		ntimer@ timer!
	end

	-1 earliest@ KeTime_SecPart + !
	999 earliest@ KeTime_MsPart + !
end

fn KeTimerInit { -- }
	pointerof KeTimerExpiration KeTimerExpirationDPC KeDPCInitialize drop

	-1 KeTimerExpirationEarliest KeTime_SecPart + !
	999 KeTimerExpirationEarliest KeTime_MsPart + !
end

fn KeTimeCompare { time1 time2 -- result }
	auto s1
	auto s2

	time1@ KeTime_SecPart + @ s1!
	time2@ KeTime_SecPart + @ s2!

	if (s1@ s2@ >)
		TIMELATER result!
		return
	end elseif (s1@ s2@ <)
		TIMEEARLIER result!
		return
	end else
		time1@ KeTime_MsPart + @ s1!
		time2@ KeTime_MsPart + @ s2!

		if (s1@ s2@ >)
			TIMELATER result!
			return
		end elseif (s1@ s2@ <)
			TIMEEARLIER result!
			return
		end

		TIMEEQUALTO result!
		return
	end
end

fn KeTimeAddMs { ms time -- }
	time@ KeTime_MsPart + @ ms +=

	auto sd
	ms@ 1000 / sd!

	1000 ms %=

	ms@ time@ KeTime_MsPart + !
	sd@ time@ KeTime_SecPart + +=
end

fn KeTimePrint { time -- }
	time@ KeTime_MsPart + @ time@ KeTime_SecPart + @ "%d sec %d ms\n" Printf
end

fn KeTimeTest { -- }
	auto time
	KeTime_SIZEOF alloc time!

	time@ HALRTCQuery

	time@ KeTimePrint
end

fn KeTimerInitialize { name function timer -- ok }
	name@ DISPATCHOBJECT_TIMER timer@ KeDispatchInitialize ok!

	if (ok@)
		return
	end

	0 timer@ KeTimer_Next + !
	0 timer@ KeTimer_Prev + !

	0 timer@ KeTimer_Enqueued + !

	function@ timer@ KeTimer_DPC + KeDPCInitialize ok!
end

fn KeTimerDequeue { timer -- ok }
	0 ok!

	auto ipl
	IPLDPC KeIPLRaise ipl!

	if (timer@ KeTimer_Enqueued + @ ~~)
		"KeTimerDequeue: attempt to dequeue a dequeued timer\n" KeCrash
	end

	0 timer@ KeTimer_Enqueued + !

	auto n
	timer@ KeTimer_Next + @ n!

	if (n@)
		timer@ KeTimer_Prev + @ n@ KeTimer_Prev + !
	end else // no next means we were the tail
		timer@ KeTimer_Prev + @ KeTimerListTail!
	end

	auto p
	timer@ KeTimer_Prev + @ p!

	if (p@)
		timer@ KeTimer_Next + @ p@ KeTimer_Next + !
	end else // no prev means we were the head
		timer@ KeTimer_Next + @ KeTimerListHead!
	end

	0 timer@ KeTimer_Next + !
	0 timer@ KeTimer_Prev + !

	ipl@ KeIPLLower
end

fn KeTimerEarliestSet { time -- }
	auto earliest
	KeTimerExpirationEarliest earliest!

	auto ipl
	IPLCLOCK KeIPLRaise ipl!

	if (time@ earliest@ KeTimeCompare TIMEEARLIER ==)
		time@ KeTime_SecPart + @ earliest@ KeTime_SecPart + !
		time@ KeTime_MsPart + @ earliest@ KeTime_MsPart + !
	end

	ipl@ KeIPLLower
end

fn KeTimerEnqueue { context1 context2 interval timer -- ok }
	0 ok!

	if (interval@ 0 ==)
		"KeTimerQueue: attempt to queue a timer with 0ms interval\n" KeCrash
	end

	auto time
	KeTime_SIZEOF alloc time!

	time@ HALRTCQuery

	auto expirytime
	timer@ KeTimer_ExpiryTime + expirytime!

	expirytime@ HALRTCQuery

	interval@ expirytime@ KeTimeAddMs

	auto ipl
	IPLDPC KeIPLRaise ipl!

	if (timer@ KeTimer_Enqueued + @)
		"KeTimerQueue: attempt to queue an enqueued timer\n" KeCrash
	end

	1 timer@ KeTimer_Enqueued + !

	context1@ timer@ KeTimer_Context1 + !
	context2@ timer@ KeTimer_Context2 + !

	auto t
	KeTimerListHead@ t!

	if (t@ ~~)
		timer@ KeTimerListHead!
		timer@ KeTimerListTail!

		expirytime@ KeTimerEarliestSet

		ipl@ KeIPLLower

		return
	end

	auto p
	auto n

	while (t@)
		t@ KeTimer_ExpiryTime + time!

		auto res
		time@ expirytime@ KeTimeCompare res!

		if (res@ TIMELATER ==)
			// this timer expires later than we do, therefore we will insert
			// ourselves before it on the list

			t@ KeTimer_Prev + @ p!
			
			if (p@)
				timer@ p@ KeTimer_Next + !
			end else
				timer@ KeTimerListHead!
			end

			p@ timer@ KeTimer_Prev + !
			t@ timer@ KeTimer_Next + !

			timer@ t@ KeTimer_Prev + !

			expirytime@ KeTimerEarliestSet

			ipl@ KeIPLLower
			return
		end

		t@ KeTimer_Next + @ t!
	end

	// there were no timers on the list that expired later than us, so we go
	// at the tail

	KeTimerListTail@ t!

	t@ timer@ KeTimer_Prev + !
	timer@ t@ KeTimer_Next + !

	timer@ KeTimerListTail!

	expirytime@ KeTimerEarliestSet

	ipl@ KeIPLLower
end

fn KeTimerPrintQueue { -- }
	auto ipl
	IPLDPC KeIPLRaise ipl!

	auto t
	KeTimerListHead@ t!

	while (t@)
		t@ KeTimer_Next + @ t!
	end

	ipl@ KeIPLLower
end