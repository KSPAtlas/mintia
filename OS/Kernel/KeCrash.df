#include "<df>/dragonfruit.h"
#include "<inc>/HALConsole.h"
#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALExit.h"

extern HALPlatformInfo { -- }

var KeCrashed 0

fn KeCrash { ... fmt -- }
	if (KeCrashed@)
		// nested crash! just loop
		while (1)
			HALCPUHalt
		end
	end

	1 KeCrashed!

	HALConsoleClear

	auto w
	auto h

	HALConsoleQuery drop drop h! w!

	"*** STOP: " Printf

	HALPlatformInfo

	'\n' HALPutc

	argv argc@ fmt@ VPrintf

	h@ 5 - w@ HALDebugDump

	'\n' HALPutc

	"An error occurred! Please take a screenshot and report the issue." w@ CenterPrint
	"The repository can be found at https://github.com/limnarch/andromeda." w@ CenterPrint

	// interrupts should be disabled, so the assumption here is that
	// HALCPUHalt will only return upon some programmer's key esque NMI.

	HALCPUHalt

	// once it does return, exit the system.

	-1 HALEXIT_SHUTDOWN HALExit
end

fn private CenterPrint { str w -- }
	auto pad
	w@ 2 / str@ strlen 2 / - pad!

	while (pad@)
		' ' HALPutc
		1 pad -=
	end

	str@ "%s\n" Printf
end