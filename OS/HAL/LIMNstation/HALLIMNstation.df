#include "<ll>/rta3x/a3x.h"
#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALExit.h"

#include "<inc>/HALDriver.h"

#include "<inc>/HALCPU.h"

#include "<inc>/HALMap.h"

#include "<inc>/HALCrash.h"

extern HALMain { ldrinfo -- ret }

externptr HALLoaderInfo

// gluing with rta3x
fn Main { ldrinfo args -- ret }
	ldrinfo@ HALMain ret!
end

var InfoTotalRAM 0

var HALLIMNstationLowBuffer 0
public HALLIMNstationLowBuffer

var HALPlatformKernelPageDirectory 0
public HALPlatformKernelPageDirectory

fn HALPlatformInfo { -- }
	// we're supposed to print platform info in this function

	InfoTotalRAM@ 1048576 / HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_PlatformModel + @ "%s (%dMB) " Printf
	HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_BusModel + @ "%s " Printf
	HALLoaderInfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_CPUModel + @ "%s " Printf
end

fn HALLIMNstationDeviceSelectNode { node -- }
	auto rs
	HALCPUInterruptDisable rs!

	pointerof a3xDeviceSelectNode
	HALLoaderInfo@ LoaderInfo_ReturnSP + @
	0
	node@
	HALCPURunInContextNoInterrupts drop drop

	rs@ HALCPUInterruptRestore
end

fn HALLIMNstationDeviceExit { -- }
	auto rs
	HALCPUInterruptDisable rs!

	pointerof a3xDeviceExit
	HALLoaderInfo@ LoaderInfo_ReturnSP + @
	0
	0
	HALCPURunInContextNoInterrupts drop drop

	rs@ HALCPUInterruptRestore
end

fn HALLIMNstationDGetProperty { name -- prop }
	HALLIMNstationLowBuffer@ name@ strcpy

	auto rs
	HALCPUInterruptDisable rs!

	pointerof a3xDGetProperty
	HALLoaderInfo@ LoaderInfo_ReturnSP + @
	0
	HALLIMNstationLowBuffer@
	HALCPURunInContextNoInterrupts drop prop!

	rs@ HALCPUInterruptRestore
end

extern HALCPUInit { -- }

extern HALLIMNstationCitronInit { ldrinfo -- }

extern HALLIMNstationLSICInit { ldrinfo -- }

fn HALPlatformInit { ldrinfo -- ret }
	0 ret!

	ldrinfo@ LoaderInfo_PlatformInfo + @ LoaderInfoPlatform_LowBuffer + @ HALLIMNstationLowBuffer!

	ldrinfo@ LoaderInfo_PageDirectory + @ HALPlatformKernelPageDirectory!

	ldrinfo@ LoaderInfo_TotalRAM + @ InfoTotalRAM!

	HALPlatformInfo

	HALCPUInit

	ldrinfo@ HALLIMNstationCitronInit

	ldrinfo@ HALLIMNstationLSICInit

	auto dll
	ldrinfo@ LoaderInfo_DLLListHead + @ dll!

	while (dll@)
		if (dll@ DLL_DriverDeviceNode + @)
			if (dll@ DLL_DriverInitEarly + @)
				auto ok

				dll@ DLL_DriverDeviceNode + @ HALLIMNstationDeviceSelectNode
					0 0 0 0 dll@ DLL_DriverInitEarly + @ FDriverInitEarly ok!
				HALLIMNstationDeviceExit

				if (ok@)
					ok@ dll@ DLL_Name + "HALPlatformInit: driver '%s' failed early initialization (%i)\n" HALCrash
				end
			end
		end

		dll@ DLL_Next + @ dll!
	end

	// auto i
	// 0 i!

	// auto pde
	// HALPlatformKernelPageDirectory@ pde!

	// while (i@ 512 <)
	// 	0 pde@!

	// 	1 i +=
	// 	4 pde +=
	// end
end

fn HALPlatformCrash { -- }
	HALPlatformMapKernelSwitch drop drop
end

extern HALLimn2500Exit { code sp -- }

extern HALLimn2500Reset { -- }

extern HALCPUExit { -- }

fn HALPlatformExit { ret mode -- }
	// MUST BE CALLED WITH INTERRUPTS DISABLED

	HALPlatformMapKernelSwitch drop drop

	HALCPUExit

	if (mode@ HALEXIT_SHUTDOWN ==)
		ret@ HALLoaderInfo@ LoaderInfo_ReturnSP + @ HALLimn2500Exit
	end elseif (mode@ HALEXIT_REBOOT ==)
		HALLimn2500Reset
	end else
		"?" Printf

		HALLimn2500Reset
	end
end