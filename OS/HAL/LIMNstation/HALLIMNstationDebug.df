#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALConsole.h"

extern HALCPUGetSP { -- sp }

fn HALPlatformDebugDump { rows cols -- }
	auto link
	HALCPUGetSP link!

	"\n" Printf

	1 rows -=

	auto infocols
	cols@ 40 / infocols!

	auto i
	infocols@ i!

	while (i@)
		"InstrPtr StackPtr DWORD+08 DWORD+12    " Printf
		1 i -=
	end

	"\n" Printf

	1 rows -=

	auto links
	0 links!

	auto pc
	pointerof HALPlatformDebugDump 4 + pc!

	while (link@ rows@ &&)
		infocols@ i!

		while (link@ i@ &&)
			if (links@ 128 >=)
				" maxtrace!\n" Printf
				return
			end

			if (link@ 3 &)
				" unaligned!\n" Printf
				return
			end

			4 pc -=

			link@ 12 + @
			link@ 8 + @
			link@
			pc@ "%08x %08x %08x %08x    " Printf

			link@ 4 + @ pc!

			link@@ link!

			1 i -=
			1 links +=
		end

		"\n" Printf

		1 rows -=
	end
end