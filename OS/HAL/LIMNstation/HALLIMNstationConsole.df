#include "<ll>/rta3x/a3x.h"
#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALConsole.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"

table Palette
	0x7FFF // 0: white
	0x07DF // 1: yellow
	0x019F // 2: orange
	0x043B // 3: red
	0x403D // 4: magenta
	0x5009 // 5: purple
	0x6400 // 6: blue 
	0x72A0 // 7: cyan
	0x0AC4 // 8: green
	0x0980 // 9: dark green
	0x04AA // 10: brown
	0x1DD2 // 11: tan
	0x5EF7 // 12: light gray
	0x4210 // 13: gray
	0x2108 // 14: dark gray
	0x0000 // 15: black
endtable

var FBAddr 0
var FBSize 0
var FBModulo 0

extern HALCPUFastDrawGlyph { bg height start mod bmp -- }

fn HALPlatformConsoleDrawChar { bmp x y -- }
	[CONSOLEBG]Palette@ HALConsoleFontHeight@ y@ HALConsoleWidthPix@ * x@ + 1 << FBAddr@ + FBModulo@ bmp@ HALCPUFastDrawGlyph
end

externptr HALLoaderInfo

fn HALPlatformConsolePutc { c -- }
	auto rs
	HALCPUInterruptDisable rs!

	auto asid
	auto pgtb

	HALPlatformMapKernelSwitch pgtb! asid!

	pointerof a3xPutc
	HALLoaderInfo@ LoaderInfo_ReturnSP + @
	0
	c@
	HALCPURunInContextNoInterrupts drop drop

	asid@ pgtb@ HALPlatformMapRestore

	rs@ HALCPUInterruptRestore
end

fn HALPlatformConsoleGetc { -- c }
	auto rs
	HALCPUInterruptDisable rs!

	auto asid
	auto pgtb

	HALPlatformMapKernelSwitch pgtb! asid!

	pointerof a3xGetc
	HALLoaderInfo@ LoaderInfo_ReturnSP + @
	0
	0
	HALCPURunInContextNoInterrupts drop c!

	asid@ pgtb@ HALPlatformMapRestore

	rs@ HALCPUInterruptRestore
end

fn HALPlatformConsoleClear { color -- }
	auto c
	[color@]Palette@ c!

	auto c32
	c@ 16 << c@ | c32!

	FBAddr@ FBSize@ c32@ memset
end

fn HALPlatformConsoleInit { ldrinfo wantmode -- havemode }
	if (wantmode@ CONSOLEMODESCREEN ==)
		if (ldrinfo@ LoaderInfo_HALConsoleFBFormat + @ BOOTFBFORMAT_AB5G5R5 ~=)
			CONSOLEMODETTY havemode!
		end else
			ldrinfo@ LoaderInfo_HALConsoleFBAddr + @ FBAddr!
			HALConsoleWidthPix@ HALConsoleHeightPix@ * 1 << FBSize!
			HALConsoleWidthPix@ HALConsoleFontWidth@ - 1 << FBModulo!

			wantmode@ havemode!
		end
	end else
		wantmode@ havemode!
	end
end