#include "<ll>/rta3x/a3x.h"
#include "<df>/dragonfruit.h"

var HALLIMNstationCitronBase 0

fn HALLIMNstationCitronInit { -- }
	"/bus/pboard/citron" a3xDeviceSelect
		"address" a3xDGetProperty HALLIMNstationCitronBase!
	a3xDeviceExit
end

fn HALLIMNstationCitronInb { port -- byte }
	port@ 4 * HALLIMNstationCitronBase@ + gb byte!
end

fn HALLIMNstationCitronIni { port -- int }
	port@ 4 * HALLIMNstationCitronBase@ + gi int!
end

fn HALLIMNstationCitronInl { port -- long }
	port@ 4 * HALLIMNstationCitronBase@ + @ long!
end

fn HALLIMNstationCitronOutb { byte port -- }
	byte@ port@ 4 * HALLIMNstationCitronBase@ + sb
end

fn HALLIMNstationCitronOuti { int port -- }
	int@ port@ 4 * HALLIMNstationCitronBase@ + si
end

fn HALLIMNstationCitronOutl { long port -- }
	long@ port@ 4 * HALLIMNstationCitronBase@ + !
end

fn HALLIMNstationCitronCommand { command port -- }
	port@ 4 * HALLIMNstationCitronBase@ + port!

	while (port@ gb 0 ~=) end

	command@ port@ sb

	while (port@ gb 0 ~=) end
end

// doesn't wait for the device to report the operation as completed before returning
fn HALLIMNstationCitronCommandASync { command port -- }
	port@ 4 * HALLIMNstationCitronBase@ + port!

	while (port@ gb 0 ~=) end

	command@ port@ sb
end