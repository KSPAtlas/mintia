#include "<df>/dragonfruit.h"
#include "<inc>/HALCrash.h"
#include "<inc>/HALConsole.h"
#include "<inc>/HALLog.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALDebug.h"
#include "<inc>/HALExit.h"

extern HALPlatformInfo { -- }

var HALCrashed 0
public HALCrashed

fn HALCrash { ... fmt -- }
	argv argc@ fmt@ HALVCrash
end

fn HALVCrash { argv argc fmt -- }
	HALCPUInterruptDisable drop

	if (HALCrashed@)
		// nested crash! just loop
		while (1)
			HALCPUHalt
		end
	end

	1 HALCrashed!

	HALConsoleClear

	auto w
	auto h

	HALConsoleQuery drop drop h! w!

	"*** STOP: " Printf

	HALPlatformInfo

	'\n' HALPutc

	argv@ argc@ fmt@ VPrintf

	h@ 5 - w@ HALDebugDump

	0 h@ 3 - HALConsoleSetCursor

	"An error occurred! Please take a screenshot and report the issue." w@ CenterPrint
	"The repository can be found at https://github.com/limnarch/andromeda." w@ CenterPrint

	0 0 HALConsoleSetCursor

	if (HALDebug ~~)
		// interrupts should be disabled, so the assumption here is that
		// HALCPUHalt will only return upon some programmer's key esque NMI.

		HALCPUHalt

		// once it does return, exit the system.
	end

	-1 HALEXIT_SHUTDOWN HALExit
end

fn private CenterPrint { str w -- }
	auto pad
	w@ 2 / str@ strlen 2 / - pad!

	while (pad@)
		' ' HALPutc
		1 pad -=
	end

	str@ "%s\n" Printf
end