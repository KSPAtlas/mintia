#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALExit.h"

#include "<inc>/HALDriver.h"

#include "<inc>/HALCrash.h"

extern HALArgsInit { ldrinfo -- }

extern HALResourceInit { ldrinfo -- ret }

extern HALConsoleInit { ldrinfo -- }

extern HALPlatformInit { ldrinfo -- ret }

extern HALResourceTest { -- }

extern HALResourceJettison { ldrinfo -- }

extern HALPlatformExit { ret mode -- }

extern HALDebugInit { ldrinfo -- }

fnptr KeMain { -- ret }

var HALLoaderInfo 0
public HALLoaderInfo

var HALLoaderPFDB 0
public HALLoaderPFDB

var HALLoaderInitialBitmap 0
public HALLoaderInitialBitmap

var HALLoaderInitialBitmapSize 0
public HALLoaderInitialBitmapSize

var HALLoaderTotalRAM 0
public HALLoaderTotalRAM

var HALLoaderHeap 0
public HALLoaderHeap

var HALLoaderHeapSize 0
public HALLoaderHeapSize

var HALInterruptStackTop 0
public HALInterruptStackTop

fn HALMain { ldrinfo -- ret }
	0 ret!

	ldrinfo@ HALLoaderInfo!

	ldrinfo@ HALArgsInit

	ldrinfo@ HALResourceInit ret!
	if (ret@)
		return
	end

	ldrinfo@ HALConsoleInit

	ldrinfo@ LoaderInfo_PageFrameDB + @ HALLoaderPFDB!

	ldrinfo@ LoaderInfo_InitMemBmp + @ HALLoaderInitialBitmap!

	ldrinfo@ LoaderInfo_InitMemBmpSz + @ HALLoaderInitialBitmapSize!

	ldrinfo@ LoaderInfo_TotalRAM + @ HALLoaderTotalRAM!

	ldrinfo@ LoaderInfo_Heap + @ HALLoaderHeap!
	ldrinfo@ LoaderInfo_HeapSize + @ HALLoaderHeapSize!

	ldrinfo@ LoaderInfo_InterruptStack + @
	ldrinfo@ LoaderInfo_InterruptStackPages + @ 12 << +
	HALInterruptStackTop!

	if (ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR ~=)
		ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR "Andromeda can't be booted with this loader: LOADERMAJOR: expected %d, got %d." HALCrash
	end elseif (ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR ~=)
		ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR "LOADERMINOR mismatch: expected %d, was given %d.\n" "HALMain" HALLog
		"Will proceed, but there may be unexpected behavior.\n" "HALMain" HALLog
	end

	"Andromeda is awake!\n" "HALMain" HALLog

	ldrinfo@ HALDebugInit

	"Platform: " "HALMain" HALLog
	ldrinfo@ HALPlatformInit ret!
	if (ret@)
		return
	end

	"\n\n" Printf

	STAGE_PRETASKING ldrinfo@ HALDriverInit

	ldrinfo@ HALResourceJettison

	ldrinfo@ LoaderInfo_KeMain + @ KeMain ret!

	ret@ HALEXIT_SHUTDOWN HALExit
end

externptr HALRTCQueryFunction

fn HALDriverInit { stage ldrinfo -- }
	auto dll
	ldrinfo@ LoaderInfo_DLLListHead + @ dll!

	while (dll@)
		if (dll@ DLL_DriverInit + @)
			if (stage@ STAGE_THREAD ==)
				dll@ DLL_Name + "initializing %s\n" "HALDriverInit" HALLog
			end

			auto ok
			stage@ dll@ DLL_DriverInit + @ FDriverInit ok!

			if (ok@)
				ok@ dll@ DLL_Name + "HALDriverInit: driver '%s' failed to initialize (%i)\n" HALCrash
			end
		end

		dll@ DLL_Next + @ dll!
	end

	if (stage@ STAGE_PRETASKING ==)
		if (HALRTCQueryFunction@ ~~)
			// we need this to have been defined by some driver

			"HALDriverInit: HALRTCQueryFunction not defined\n" HALCrash
		end
	end
end

fn HALExit { ret mode -- }
	ret@ mode@ "Exit (%d): %i.\n" "HALExit" HALLog

	ret@ mode@ HALPlatformExit
end