#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALLog.h"

extern HALArgsInit { ldrinfo -- }

extern HALResourceInit { ldrinfo -- ret }

extern HALConsoleInit { ldrinfo -- }

extern HALPlatformInit { ldrinfo -- ret }

extern HALResourceTest { -- }

extern HALResourceJettison { ldrinfo -- }

fnptr KeMain { -- ret }

struct DLL
	4 Next
	128 Name
endstruct

fn HALMain { ldrinfo -- ret }
	0 ret!

	ldrinfo@ HALArgsInit

	ldrinfo@ HALResourceInit ret!
	if (ret@)
		return
	end

	ldrinfo@ HALConsoleInit

	if (ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR ~=)
		ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR "LOADERMAJOR mismatch: expected %d, was given %d.\n" "HALMain" HALLog
		"Andromeda can't be booted with this loader.\n" Printf
		return
	end elseif (ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR ~=)
		ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR "LOADERMINOR mismatch: expected %d, was given %d.\n" "HALMain" HALLog
		"Will proceed, but there may be unexpected behavior.\n" "HALMain" HALLog
	end

	"Andromeda is awake!\n" "HALMain" HALLog

	"Platform: " "HALMain" HALLog
	ldrinfo@ HALPlatformInit ret!
	if (ret@)
		return
	end

	"\n" Printf

	"Loaded modules: \n" "HALMain" HALLog

	auto dll
	ldrinfo@ LoaderInfo_DLLListHead + @ dll!

	while (dll@)
		dll@ DLL_Name + "  %s\n" "HALMain" HALLog

		dll@ DLL_Next + @ dll!
	end

	ldrinfo@ HALResourceJettison

	"\n" Printf

	ldrinfo@ LoaderInfo_KeMain + @ KeMain ret!
end