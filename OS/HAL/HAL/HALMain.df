#include "<df>/dragonfruit.h"
#include "../../Loader/LoaderGlobal.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALExit.h"

extern HALArgsInit { ldrinfo -- }

extern HALResourceInit { ldrinfo -- ret }

extern HALConsoleInit { ldrinfo -- }

extern HALPlatformInit { ldrinfo -- ret }

extern HALResourceTest { -- }

extern HALResourceJettison { ldrinfo -- }

extern HALPlatformExit { ret mode -- }

fnptr KeMain { -- ret }

var HALLoaderInfo 0
public HALLoaderInfo

fn HALMain { ldrinfo -- ret }
	0 ret!

	ldrinfo@ HALLoaderInfo!

	ldrinfo@ HALArgsInit

	ldrinfo@ HALResourceInit ret!
	if (ret@)
		return
	end

	ldrinfo@ HALConsoleInit

	if (ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR ~=)
		ldrinfo@ LoaderInfo_Major + @ LOADERMAJOR "LOADERMAJOR mismatch: expected %d, was given %d.\n" "HALMain" HALLog
		"Andromeda can't be booted with this loader.\n" Printf
		return
	end elseif (ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR ~=)
		ldrinfo@ LoaderInfo_Minor + @ LOADERMINOR "LOADERMINOR mismatch: expected %d, was given %d.\n" "HALMain" HALLog
		"Will proceed, but there may be unexpected behavior.\n" "HALMain" HALLog
	end

	"Andromeda is awake!\n" "HALMain" HALLog

	"Platform: " "HALMain" HALLog
	ldrinfo@ HALPlatformInit ret!
	if (ret@)
		return
	end

	ldrinfo@ HALResourceJettison

	"\n\n" Printf

	ldrinfo@ LoaderInfo_KeMain + @ KeMain ret!

	ret@ HALEXIT_SHUTDOWN HALExit
end

fn HALExit { ret mode -- }
	ret@ mode@ "Exit (%d): %i.\n" "HALExit" HALLog

	ret@ mode@ HALPlatformExit
end

fnptr DebugEntry { -- }

fn HALDebug { -- ran }
	auto debugger
	HALLoaderInfo@ LoaderInfo_DebuggerEntry + @ debugger!

	0 ran!

	if (debugger@)
		1 ran!
		debugger@ DebugEntry
	end
end

extern HALPlatformDebugDump { rows cols -- }

fn HALDebugDump { rows cols -- }
	if (rows@ 5 >=)
		"\n" Printf

		1 rows -=

		auto infocols
		cols@ 40 / infocols!

		auto i
		infocols@ i!

		while (i@)
			"DLL Base TimeStmp  Name                " Printf
			1 i -=
		end

		"\n" Printf

		1 rows -=

		auto dll
		HALLoaderInfo@ LoaderInfo_DLLListHead + @ dll!

		while (dll@ rows@ &&)
			infocols@ i!

			while (dll@ i@ &&)
				auto loff
				dll@ DLL_LOFF + loff!

				dll@ DLL_Name + loff@ LOFFLoaded_Timestamp + @ loff@ LOFFLoaded_TextRealAddr + @ "%08x %08x  %19s " Printf

				1 i -=
				dll@ DLL_Next + @ dll!
			end

			"\n" Printf

			1 rows -=
		end
	end

	rows@ cols@ HALPlatformDebugDump
end